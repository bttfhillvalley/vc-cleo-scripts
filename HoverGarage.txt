{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
const
    TEXTURE_OFFSET = 28
end
0000:
thread 'FLYMENU'
jump @WaitMenu

{$INCLUDE Menu/Common.txt}

:WaitMenu
wait 0
0AB4: vehicle = var TIME_MACHINE
0AB4: vehicle_flags = var TM_FLAGS
0AB4: garage_option = var IN_GARAGE
if and
    not Car.Wrecked(vehicle)
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    if
        garage_option == HOVER_GARAGE
    then
        jump @StartMenu
    end
end
jump @WaitMenu

:StartMenu
3F84: play_sound "doc garage/page1.wav" loop 0
select = 1
last_select = 0
num_options = 2
0AB3: var IN_GARAGE = HOVER_GARAGE
02D4: car vehicle turn_off_engine
041D: set_camera_near_clip 0.1
Player.CanMove($PLAYER_CHAR, false)

// Fix flying circuits
0B10: vehicle_flags = vehicle_flags AND TC_FRIED_BITMASK
timera = 0

:OpenMenu
wait 0
0AB4: garage_option = var IN_GARAGE
if
    garage_option == HOVER_GARAGE
jf @WaitMenu
if or
    Car.Wrecked(vehicle)
    not Actor.InCar($PLAYER_ACTOR, vehicle)
then
    Camera.Restore()
    041D: set_camera_near_clip 0.9
    Player.CanMove($PLAYER_CHAR, true)
    jump @WaitMenu
end
if and
    select == num_options
    is_key_pressed KEY_ENTER
then
    3F84: play_sound "doc garage/page2.wav" loop 0
    0AB3: var IN_GARAGE = NO_GARAGE
    Camera.Restore()
    gosub @UpdateModel
    wait 100
    041D: set_camera_near_clip 0.9
    3F03: car vehicle turn_on_engine
    Player.CanMove($PLAYER_CHAR, true)
    3F2C: detach_car vehicle
    3F0C: set_car vehicle collision 1
    jump @WaitMenu
end
if
    timera > 100
then
    if
        is_key_pressed KEY_LEFT
    then
        0B10: enabled = vehicle_flags AND REAR_BIT
        gosub @UpdateModel
        if
            enabled == REAR_NONE_BIT
        then
            0AB3: var IN_GARAGE = DOC_GARAGE
        else
            0AB3: var IN_GARAGE = FUSION_GARAGE
        end
        jump @WaitMenu
    else if
        is_key_pressed KEY_RIGHT
    then
        gosub @UpdateModel
        0AB3: var IN_GARAGE = DOC_GARAGE
        jump @WaitMenu
    end
    end
end


// Handle Selection
gosub @HandleKeys
gosub @FixSelection

// Show Menu
0B10: enabled = vehicle_flags AND FC_BIT
0B10: wheels = vehicle_flags AND WHEELS_BIT
if
    wheels == WHEELS_TRAIN_BIT
then
    enabled = 0

end
if
    enabled > 0
then
    038F: load_texture 'HVGRG2' as 28 // Hover garage
    enabled = 1
else
    038F: load_texture 'HVGRG1' as 28 // Hover garage
end

cleo_call @DrawTexture params_count 9 TEXTURE_OFFSET 160.0 180.0 300.0 325.0 255 255 255 255

gosub @CheckHover
gosub @CheckExit

// Limit options
last_select = select
jump @OpenMenu

:CheckHover
0B10: enabled = vehicle_flags AND FC_BIT
0B10: wheels = vehicle_flags AND WHEELS_BIT
if
    select == 1
then
    cleo_call @DrawTexture params_count 9 CURSOR 155.0 210.0 115.0 11.0 255 255 255 255
    if and
        last_key == 0
        is_key_pressed KEY_ENTER
    then
        if or
            enabled == 0
            not wheels == WHEELS_TRAIN_BIT
        then
            0B12: vehicle_flags = vehicle_flags XOR FC_BIT
        end
        gosub @CheckRR
        gosub @ToggleOption
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset -1.5 -4.0 -0.5 point_at 0.0 0.0 0.0
    end
end
return

:CheckRR
0B10: enabled = vehicle_flags AND FC_BIT
0B10: wheels = vehicle_flags AND WHEELS_BIT
if and
    not enabled == 0
    wheels == WHEELS_TRAIN_BIT
then
    0B11: vehicle_flags = vehicle_flags OR WHEELS_TRAIN_HOVER_BIT
end
return

:CheckExit
if
    select == 2
then
    cleo_call @DrawTexture params_count 9 CURSOR 150.0 330.0 105.0 11.0 255 255 255 255
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset -3.5 -5.0 1.0 point_at -1.0 0.0 0.0
    end
end
return

{$INCLUDE Common/DrawTexture.txt}
