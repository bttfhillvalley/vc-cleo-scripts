{$CLEO .s}
{$INCLUDE variables.txt}
{$INCLUDE ../Common/CleoConstants.txt}
:CheckRearDoorToggle
if
    timera < 500
then
    return
end
if
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    if
        80E1:   not key_pressed 0 19
    then
        return
    end
else
    cleo_call @CheckRearDoorPos 1 vehicle ret door_check
    if or
        80E1:   not key_pressed 0 4
        door_check == false
    then
        return
    end
end
timera = 0

// Toggle rear_door animations
rear_door *= -1
rear_door += 1
return

:CheckRearDoorPos
0407: create_coordinate 1@ 2@ 3@ from_car vehicle offset -1.5 -4.0 0.0
0407: create_coordinate 4@ 5@ 6@ from_car vehicle offset 1.5 -4.0 0.0
if or
    00FF:   actor $PLAYER_ACTOR 0 1@ 2@ 3@ radius 0.75 0.75 2.0
    00FF:   actor $PLAYER_ACTOR 0 4@ 5@ 6@ radius 0.75 0.75 2.0
then
    cleo_return 1 true
end
cleo_return 1 false

:FrameControl
if and
    rear_door == 0
    frame > 0
then
    frame -= 1
    gosub @PlayDoorSound
else if and
    rear_door == 1
    frame < 600
then
    frame += 1
    gosub @PlayDoorSound
else
    gosub @StopDoorSound
end
end
return

:PlayDoorSound
if
    3F93: is_sound_stopped "GMCVan/truck_ramp.wav" index vehicle
then
    3F86: attach_sound "GMCVan/truck_ramp.wav" to_car vehicle pos 0.0 0.0 0.0 loop 0 size 10.0
end
return

:StopDoorSound
if
    3F92: is_sound_playing "GMCVan/truck_ramp.wav" index vehicle
then
    3F91: stop_sound "GMCVan/truck_ramp.wav" index vehicle
end
return


:RearDoorAnimation
// Ramp rear_door
0093: rear_x = frame
rear_x /= 6.0
3F16: set_car vehicle component "rampsdummy" angle rear_x 0.0 0.0
rear_x *= -1.0
3F16: set_car vehicle component "stabiliser" angle rear_x 0.0 0.0

// Ramps
ramp_x = rear_x
rear_x *= -1.0
repeat
    gosub @CalculateRampClearance
until height >= GROUND_OFFSET
if
    ramp_x > -2.0
then
    ramp_x = -2.0
end

3F16: set_car vehicle component "ramp" angle ramp_x 0.0 0.0

// Rods
0093: rear_x = frame
rear_x *= -0.1
rear_x += 140.0
3F16: set_car vehicle component "rods" angle rear_x 0.0 0.0
return

:CalculateRampClearance
temp_x = rear_x
02F7: height = cosine temp_x // float
height *= RAMP_LENGTH
005B: temp_x += ramp_x
02F7: temp_x = cosine temp_x
temp_x *= RAMP_LENGTH
height -= temp_x
height += BASE_HEIGHT
if
    height < GROUND_OFFSET
then
    ramp_x -= 0.01
end
return