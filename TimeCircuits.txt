{$CLEO .s}
{$INCLUDE Common/CleoConstants.txt}
0000:
thread 'T_CIRC'
int vehicle
int vehicle_flags
int dest_date
int dest_time
int past_date
int past_time
int curr_date
int curr_time
float tcd_angle
int tcon_var
int fried_var
int travel_var
int flash_var
int health
int rand
const
    DPOS = 200.0
    DR = 255
    DG = 80
    DB = 30

    PPOS = 243.0
    PR = 70
    PG = 240
    PB = 10

    LPOS = 286.0
    LR = 240
    LG = 190
    LB = 40

    MONTH_POS = 650.0
    DAY_POS = 680.0
    YEAR_POS = 716.0
    HOUR_POS = 788.0
    MINUTE_POS = 824.0
end
0B10: tcon_var = vehicle_flags AND TC_BIT       // Time Circuits status
0B10: fried_var = vehicle_flags AND TC_FRIED_BIT // Time Circuits Fried
3F10: set_car vehicle component "tcdswitchlighton" visible 0
3F10: set_car vehicle component "tcdswitchlightoff" visible 0
gosub @TimeCircuitsReset
stream_custom_script "TimeCircuits/Keypad.s" vehicle

:TimeCircuitsStart
wait 10
if
    Car.Wrecked(vehicle)
then
    terminate_this_custom_script
end

// Store any globals if driving
if
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    if
        timerb > 100
    then
        0AB4: dest_date = var DDATE
        0AB4: dest_time = var DTIME
        0AB4: past_date = var PDATE
        0AB4: past_time = var PTIME
        0AB4: tcon_var  = var TCON
        0AB4: fried_var = var FRIED
        0AB4: travel_var = var TRAVEL
    end
    health = Car.Health(vehicle)

    // Time Circuits glitch when damaged
    if and
        not tcon_var == 0
        fried_var == 0
        travel_var == 0
        health < 650
    then
        0209: rand = random_int_in_ranges 1 10000
        if
            rand == 5000
        then
            stream_custom_script "TimeCircuits/Error.s"
        end
    end
else
    timerb = 0
end

// Draw Time Circuit Display Image
cleo_call @DrawFrame 2 vehicle tcon_var

// Handle time circuit switch
gosub @TimeCircuitsSwitch

// Display colons
gosub @TimeCircuitsColons

//first, hide all components / put them to base configuration
if or
    tcon_var == 0
    fried_var == 1
then
    // Turn off Time Circuits if visible
    if or
        3F40: is_car vehicle component "dtam" visible
        3F40: is_car vehicle component "dtpm" visible
    then
        gosub @TimeCircuitsReset
    end
    jump @TimeCircuitsStart
end


// Destination time
cleo_call @TimeCircuitsLine params_count 4 vehicle dest_date dest_time 0

// Present time
0AB4: curr_date = var CDATE
0AB4: curr_time = var CTIME
cleo_call @TimeCircuitsLine params_count 4 vehicle curr_date curr_time 1

// Last Time Departed
cleo_call @TimeCircuitsLine params_count 4 vehicle past_date past_time 2
jump @TimeCircuitsStart

:TimeCircuitsLine
if
    1@ == -1 // integer values
then
    4@ = -1
    5@ = -1
    6@ = -1
    7@ = -1
    8@ = -1
else
    0085: 4@ = 1@
    0085: 5@ = 1@ // integer values and handles
    0085: 6@ = 1@
    4@ /= 100
    6@ /= 100
    6@ *= 100
    0062: 5@ -= 6@
    6@ /= 10000
    6@ *= 100
    0062: 4@ -= 6@
    6@ /= 100
    008B: 7@ = 2@ // integer values and handles
    008B: 8@ = 2@ // integer values and handles
    7@ /= 100
    7@ *= 100
    0062: 8@ -= 7@
    7@ /= 100
end

0AB4: 14@ = var FLASH
0AB4: 15@ = var TCON
if
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    if and
        15@ == 0x1F
        3@ == 0
    then
        // This handles flashing the destination time on input
        0AB4: flash_var = var FLASH
        0B13: flash_var = NOT flash_var
    else
        0AB4: flash_var = var TCON
    end
    0B10: 15@ = flash_var AND 0x8
    if
        15@ == 0
    then
        4@ = -1
    end
    0B10: 15@ = flash_var AND 0x4
    if
        15@ == 0
    then
        5@ = -1
    end
    0B10: 15@ = flash_var AND 0x2
    if
        15@ == 0
    then
        6@ = -1
    end
    0B10: 15@ = flash_var AND 0x1
    if
        15@ == 0
    then
        7@ = -1
        8@ = -1
    end
end
cleo_call @TimeCircuitsMonth params_count 3 vehicle 3@ 4@
cleo_call @TimeCircuitsDay params_count 3 vehicle 3@ 5@
cleo_call @TimeCircuitsYear params_count 3 vehicle 3@ 6@
cleo_call @TimeCircuitsHour params_count 3 vehicle 3@ 7@
cleo_call @TimeCircuitsMinute params_count 3 vehicle 3@ 8@
cleo_return 0

:TimeCircuitsMonth
if
    1@ == 0
then
    3F55: set_car vehicle component "dtmonth" digit 2@ on
    cleo_call @TimeCircuitsDrawMonth params_count 7 vehicle DR DG DB MONTH_POS DPOS 2@
else if
    1@ == 1
then
    3F55: set_car vehicle component "ptmonth" digit 2@ on
    cleo_call @TimeCircuitsDrawMonth params_count 7 vehicle PR PG PB MONTH_POS PPOS 2@
else
    3F55: set_car vehicle component "ltdmonth" digit 2@ on
    cleo_call @TimeCircuitsDrawMonth params_count 7 vehicle LR LG LB MONTH_POS LPOS 2@
end
end
cleo_return 0

:TimeCircuitsDay
gosub @DivideTwo
if
    1@ == 0
then
    3F55: set_car vehicle component "dtday2" digit 2@ on
    3F55: set_car vehicle component "dtday1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle DR DG DB DAY_POS DPOS 2@ 3@
else if
    1@ == 1
then
    3F55: set_car vehicle component "ptday2" digit 2@ on
    3F55: set_car vehicle component "ptday1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle PR PG PB DAY_POS PPOS 2@ 3@
else
    3F55: set_car vehicle component "ltdday2" digit 2@ on
    3F55: set_car vehicle component "ltdday1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle LR LG LB DAY_POS LPOS 2@ 3@
end
end
cleo_return 0

:TimeCircuitsYear
gosub @DivideFour
if
    1@ == 0
then
    3F55: set_car vehicle component "dtyear4" digit 2@ on
    3F55: set_car vehicle component "dtyear3" digit 3@ on
    3F55: set_car vehicle component "dtyear2" digit 4@ on
    3F55: set_car vehicle component "dtyear1" digit 5@ on
    cleo_call @TimeCircuitsDraw4Digits params_count 10 vehicle DR DG DB YEAR_POS DPOS 2@ 3@ 4@ 5@
else if
    1@ == 1
then
    3F55: set_car vehicle component "ptyear4" digit 2@ on
    3F55: set_car vehicle component "ptyear3" digit 3@ on
    3F55: set_car vehicle component "ptyear2" digit 4@ on
    3F55: set_car vehicle component "ptyear1" digit 5@ on
    cleo_call @TimeCircuitsDraw4Digits params_count 10 vehicle PR PG PB YEAR_POS PPOS 2@ 3@ 4@ 5@
else
    3F55: set_car vehicle component "ltdyear4" digit 2@ on
    3F55: set_car vehicle component "ltdyear3" digit 3@ on
    3F55: set_car vehicle component "ltdyear2" digit 4@ on
    3F55: set_car vehicle component "ltdyear1" digit 5@ on
    cleo_call @TimeCircuitsDraw4Digits params_count 10 vehicle LR LG LB YEAR_POS LPOS 2@ 3@ 4@ 5@
end
end
cleo_return 0

:TimeCircuitsHour
if
    1@ == 0
then
    cleo_call @ShowAmPm 4 vehicle "dtam" "dtpm" 2@
    cleo_call @TimeCircuitsDots params_count 6 vehicle DR DG DB DPOS 2@
else if
    1@ == 1
then
    cleo_call @ShowAmPm 4 vehicle "ptam" "ptpm" 2@
    cleo_call @TimeCircuitsDots params_count 6 vehicle PR PG PB PPOS 2@
else
    cleo_call @ShowAmPm 4 vehicle "ltdam" "ltdpm" 2@
    cleo_call @TimeCircuitsDots params_count 6 vehicle LR LG LB LPOS 2@
end
end
gosub @TimeCircuitsHourAdjust
gosub @DivideTwo
if
    1@ == 0
then
    3F55: set_car vehicle component "dthour2" digit 2@ on
    3F55: set_car vehicle component "dthour1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle DR DG DB HOUR_POS DPOS 2@ 3@
else if
    1@ == 1
then
    3F55: set_car vehicle component "pthour2" digit 2@ on
    3F55: set_car vehicle component "pthour1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle PR PG PB HOUR_POS PPOS 2@ 3@
else
    3F55: set_car vehicle component "ltdhour2" digit 2@ on
    3F55: set_car vehicle component "ltdhour1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle LR LG LB HOUR_POS LPOS 2@ 3@
end
end
cleo_return 0

:TimeCircuitsMinute
gosub @DivideTwo
if
    1@ == 0
then
    3F55: set_car vehicle component "dtmin2" digit 2@ on
    3F55: set_car vehicle component "dtmin1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle DR DG DB MINUTE_POS DPOS 2@ 3@
else if
    1@ == 1
then
    3F55: set_car vehicle component "ptmin2" digit 2@ on
    3F55: set_car vehicle component "ptmin1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle PR PG PB MINUTE_POS PPOS 2@ 3@
else
    3F55: set_car vehicle component "ltdmin2" digit 2@ on
    3F55: set_car vehicle component "ltdmin1" digit 3@ on
    cleo_call @TimeCircuitsDraw2Digits params_count 8 vehicle LR LG LB MINUTE_POS LPOS 2@ 3@
end
end
cleo_return 0

:ShowAmPm
if
    3@ < 12
then
    if
        3F50:   not set_car vehicle component 1@ visible
    then
        3F10: set_car vehicle component 1@ visible 1
        3F10: set_car vehicle component 2@ visible 0
    end
else
    if
        3F50:   not set_car vehicle component 2@ visible
    then
        3F10: set_car vehicle component 1@ visible 0
        3F10: set_car vehicle component 2@ visible 1
    end
end
cleo_return 0

:DivideTwo
if
    2@ == -1
then
    3@ = -1
else
    0085: 3@ = 2@
    2@ /= 10
    2@ *= 10
    0062: 3@ -= 2@
    2@ /= 10
end
return

:DivideFour
if
    2@ == -1
then
    3@ = -1
    4@ = -1
    5@ = -1
else
    0085: 3@ = 2@
    3@ /= 100
    0085: 4@ = 2@
    4@ /= 10
    0085: 5@ = 2@
    2@ /= 10
    2@ *= 10
    0062: 5@ -= 2@
    2@ /= 100
    2@ *= 10
    0062: 4@ -= 2@
    2@ /= 100
    2@ *= 10
    0062: 3@ -= 2@
    2@ /= 10
end
return

:TimeCircuitsDrawMonth
0AB4: 13@ = var IN_GARAGE
0AB4: 14@ = var HUD_DISP
0AB4: 15@ = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    13@ == 0
    14@ == 1
    15@ == 0
then
    if
        6@ < 0
    then
        6@ = 0
    end
    6@ += 41
    cleo_call @DrawTexture params_count 9 6@ 4@ 5@ 41.0 18.0 1@ 2@ 3@ 255
end
cleo_return 0

:TimeCircuitsDraw2Digits
0AB4: 13@ = var IN_GARAGE
0AB4: 14@ = var HUD_DISP
0AB4: 15@ = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    13@ == 0
    14@ == 1
    15@ == 0
then
    gosub @TimeCircuitsTextureOffset
    cleo_call @DrawTexture params_count 9 6@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
    4@ += 12.0
    cleo_call @DrawTexture params_count 9 7@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
end
cleo_return 0

:TimeCircuitsDraw4Digits
0AB4: 13@ = var IN_GARAGE
0AB4: 14@ = var HUD_DISP
0AB4: 15@ = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    13@ == 0
    14@ == 1
    15@ == 0
then
    gosub @TimeCircuitsTextureOffset
    cleo_call @DrawTexture params_count 9 6@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
    4@ += 12.0
    cleo_call @DrawTexture params_count 9 7@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
    4@ += 12.0
    cleo_call @DrawTexture params_count 9 8@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
    4@ += 12.0
    cleo_call @DrawTexture params_count 9 9@ 4@ 5@ 12.0 18.0 1@ 2@ 3@ 255
end
cleo_return 0

:TimeCircuitsTextureOffset
// TXD Offsets
6@ += 30
7@ += 30
8@ += 30
9@ += 30
return

:TimeCircuitsHourAdjust
if
    2@ > 12
then
    2@ -= 12
end

if
    2@ == 0
then
    2@ = 12
end
return

:TimeCircuitsDots
// Red Destination dots are Amber
0AB4: 13@ = var IN_GARAGE
0AB4: 14@ = var HUD_DISP
0AB4: 15@ = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    13@ == 0
    14@ == 1
    15@ == 0
then
    if
        1@ == 255
    then
        2@ = 255
    end

    // AM/PM Dot
    if
        5@ < 12
    then
        4@ -= 6.0
        cleo_call @DrawTexture params_count 9 54 771.0 4@ 6.0 8.0 1@ 2@ 3@ 255
        4@ += 6.0
    else
        4@ += 5.0
        cleo_call @DrawTexture params_count 9 54 771.0 4@ 6.0 8.0 1@ 2@ 3@ 255
        4@ -= 5.0
    end

    // Flash Colon
    if
        timera < 500 // integer values
    then
        4@ -= 2.0
        cleo_call @DrawTexture params_count 9 54 812.5 4@ 6.0 8.0 1@ 2@ 3@ 255
        4@ += 6.0
        cleo_call @DrawTexture params_count 9 54 812.5 4@ 6.0 8.0 1@ 2@ 3@ 255
        4@ -= 4.0
    end
end
cleo_return 0

:TimeCircuitsSwitch
if
    fried_var == 1
then
    if or
        3F40:   not is_car vehicle component "tcdswitchlighton" visible
        3F40:   not is_car vehicle component "tcdswitchlightoff" visible
    then
        3F10: set_car vehicle component "tcdswitchlighton" visible 0
        3F10: set_car vehicle component "tcdswitchlightoff" visible 0
        3F10: set_car vehicle component "tcdkeypadlightson" visible 0
    end
end
if
    tcon_var == 0
then
    // Switch Lights
    if and
        fried_var == 0
        3F50:   not is_car vehicle component "tcdswitchlightoff" visible
    then
        3F10: set_car vehicle component "tcdswitchlighton" visible 0
        3F10: set_car vehicle component "tcdswitchlightoff" visible 1
        3F10: set_car vehicle component "tcdkeypadlightson" visible 0
    end

    // Time Circuits Handle
    if
        tcd_angle < 0.0
    then
        tcd_angle += 10.0
        3F16: set_car vehicle component "tcdhandle" angle 0.0 tcd_angle 0.0
    end
else
    // Switch Lights
    if and
        fried_var == 0
        3F50:   not is_car vehicle component "tcdswitchlighton" visible
    then
        3F10: set_car vehicle component "tcdswitchlighton" visible 1
        3F10: set_car vehicle component "tcdswitchlightoff" visible 0
        3F10: set_car vehicle component "tcdkeypadlightson" visible 1
    end

    // Time Circuits Handle
    if
        tcd_angle > -60.0
    then
        tcd_angle -= 10.0
        3F16: set_car vehicle component "tcdhandle" angle 0.0 tcd_angle 0.0
    end
end
return

:TimeCircuitsColons
if
    timera > 999
then
    timera -= 1000
    if and
        not tcon_var == 0
        fried_var == 0
    then
        3F86: attach_sound "delorean/timecircuits/colon_beep.wav" to_car vehicle offset 0 0.32 0.14 loop 0 size 1.0
    end
end
if and
    timera < 500
    not tcon_var == 0
    fried_var == 0
then
    if
        3F50:   not is_car vehicle component "dtcolon" visible
    then
        3F10: set_car vehicle component "dtcolon" visible 1
        3F10: set_car vehicle component "ptcolon" visible 1
        3F10: set_car vehicle component "ltdcolon" visible 1
    end
else
    if
        3F40:   is_car vehicle component "dtcolon" visible
    then
        3F10: set_car vehicle component "dtcolon" visible 0
        3F10: set_car vehicle component "ptcolon" visible 0
        3F10: set_car vehicle component "ltdcolon" visible 0
    end
end
return

:DrawFrame
0AB4: 2@ = var IN_GARAGE
0AB4: 3@ = var HUD_DISP
0AB4: 4@ = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    not 1@ == 0
    2@ == 0
    3@ == 1
    4@ == 0
then
    cleo_call @DrawTexture params_count 9 1 733.0 242.0 216.0 128.0 255 255 255 255
end
cleo_return 0

:TimeCircuitsReset
// Month
for 15@ = 1 to 12
	3F11: set_car vehicle component "dtmonth" index 15@ visible 0
	3F11: set_car vehicle component "ptmonth" index 15@ visible 0
	3F11: set_car vehicle component "ltdmonth" index 15@ visible 0
end

// Day
for 15@ = 10 to 29
	3F11: set_car vehicle component "dtday" index 15@ visible 0
	3F11: set_car vehicle component "ptday" index 15@ visible 0
	3F11: set_car vehicle component "ltdday" index 15@ visible 0
end

// Year
for 15@ = 10 to 49
	3F11: set_car vehicle component "dtyear" index 15@ visible 0
	3F11: set_car vehicle component "ptyear" index 15@ visible 0
	3F11: set_car vehicle component "ltdyear" index 15@ visible 0
end

// Hour
for 15@ = 10 to 29
	3F11: set_car vehicle component "dthour" index 15@ visible 0
	3F11: set_car vehicle component "pthour" index 15@ visible 0
	3F11: set_car vehicle component "ltdhour" index 15@ visible 0
end

// Minute
for 15@ = 10 to 29
	3F11: set_car vehicle component "dtmin" index 15@ visible 0
	3F11: set_car vehicle component "ptmin" index 15@ visible 0
	3F11: set_car vehicle component "ltdmin" index 15@ visible 0
end

// AM
3F10: set_car vehicle component "dtam" visible 0
3F10: set_car vehicle component "ptam" visible 0
3F10: set_car vehicle component "ltdam" visible 0

// PM
3F10: set_car vehicle component "dtpm" visible 0
3F10: set_car vehicle component "ptpm" visible 0
3F10: set_car vehicle component "ltdpm" visible 0
return

{$INCLUDE Common/DrawTexture.txt}
