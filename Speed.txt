{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
0000:
thread 'SPDMTR'
int vehicle
int vehicle_model
float vehicle_speed
float virtual_speed
int handbrake
int tens
int ones
int decimal
int texture_offset
float x_offset
float x
int var1
int var2
int var3
int var4


while true
    wait 10
    if or
        not Player.Defined($PLAYER_CHAR)
        80E0:   not player $PLAYER_CHAR driving
        is_camera_in_widescreen_mode
    then
        continue
    end
    if
        Player.InRemoteMode($PLAYER_CHAR)
    then
        vehicle = Player.RC_car($PLAYER_CHAR)
    else
        03C1: vehicle = player $PLAYER_CHAR car
    end

    // Don't show speedo's on skateboards
    vehicle_model = Car.Model(vehicle)
    if or
        00DD:   actor $PLAYER_ACTOR driving_vehicle_type BOARD_MODEL
        00DD:   actor $PLAYER_ACTOR driving_vehicle_type HANDLEBAR_MODEL
    then
        continue
    end
    02E3: vehicle_speed = car vehicle speed

    //check if the car is in remote mode
    3F66: get_joypad 0 throttle_to var1 var2
    if
        3F68: is_car vehicle doing_burnout
    then
        handbrake = 1 //remember that the car is stopped
        if
            //max virtual speed limit chosen at random
            virtual_speed < 35.15 // 64.5mph
        then
            //increase the virtual speed
            virtual_speed += 0.1
        end

    else
        if
            handbrake == 1 //car is stopped
        then
            //release the stop and launch the car
            //04BA: set_car vehicle speed_instantly virtual_speed
            stream_custom_script "LineLock.s" vehicle virtual_speed
            handbrake = 0 // reset the stop so we only once set the speed from the burnout
            //virtual_speed = 0
        end
        if
            var1 >= 150
        then
            if
                virtual_speed >= 0.0
            then
                virtual_speed -= 1.55
            end
            if
                virtual_speed < 0.0
            then
                virtual_speed = 0.0
            end
        else
            virtual_speed = 0.0
        end
    end

    if
        Player.InRemoteMode($PLAYER_CHAR)
    then
        vehicle_speed += virtual_speed
    end

    // Parse speed into separate digits for rendering
    vehicle_speed *= 18.35
    if
        vehicle_speed > 880.0
    then
        vehicle_speed = 880.0
    end
    008C: decimal = float_to_integer vehicle_speed
    tens = decimal
    tens /= 100
    tens *= 100
    decimal -= tens
    tens /= 100

    ones = decimal
    ones /= 10
    ones *= 10
    decimal -= ones
    ones /= 10
    if
        Player.InRemoteMode($PLAYER_CHAR)
    then
        3F0B: replace_tex "futaba" "y1" with "futaba" "ny" index tens
        3F0B: replace_tex "futaba" "y2" with "futaba" "ny" index ones
        3F0B: replace_tex "futaba" "y3" with "futaba" "ny" index decimal
    end
    0AB4: var1 = var IN_GARAGE
    0AB4: var2 = var HUD_DISP
    0AB4: var3 = var TRAVEL
    0AB4: var4 = var CUTSCENE
    if and
        var1 == 0
        var2 == 1
        var3 == 0
        var4 == 0
    then
        if
            Player.InRemoteMode($PLAYER_CHAR)
        then
            texture_offset = 17
            x_offset = 715.0
        else
            texture_offset = 5
            x_offset = 760.0
        end

        texture_offset += 10
        // Dot
        x = x_offset
        x += 65.0
        cleo_call @DrawTexture params_count 9 texture_offset x 470.0 10.0 10.0 255 255 255 255

        // Background
        texture_offset += 1
        x = x_offset
        cleo_call @DrawTexture params_count 9 texture_offset x 440.0 40.0 60.0 255 255 255 255
        x += 45.0
        cleo_call @DrawTexture params_count 9 texture_offset x 440.0 40.0 60.0 255 255 255 255
        if
            Player.InRemoteMode($PLAYER_CHAR)
        then
            x += 45.0
            cleo_call @DrawTexture params_count 9 texture_offset x 440.0 40.0 60.0 255 255 255 255
        end

        // Reset offset
        texture_offset -= 11
        x = x_offset

        // tens digit
        if
            tens > 0
        then
            tens += texture_offset
            cleo_call @DrawTexture params_count 9 tens x 440.0 40.0 60.0 255 255 255 255
        else
            if
                Player.InRemoteMode($PLAYER_CHAR)
            then
                3F0A: replace_tex "futaba" "y1" with "futaba" "nyblank0"
            end
        end

        // ones digit
        ones += texture_offset
        x += 45.0
        cleo_call @DrawTexture params_count 9 ones x 440.0 40.0 60.0 255 255 255 255

        // decimal digit
        if
            Player.InRemoteMode($PLAYER_CHAR)
        then
            decimal += texture_offset
            x += 45.0
            cleo_call @DrawTexture params_count 9 decimal x 440.0 40.0 60.0 255 255 255 255
        end
    end
end

{$INCLUDE Common/DrawTexture.txt}
