{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
const
    PLUTONIUM_MODEL = 6948
end
0000:
int pickup
int lybian
int driver
int gunner
int lybian_health
int gunner_mode
int result
int marker
int enrage
float x
float y
float z
float angle
float px
float py
float pz

:GetPlutoniumStart
wait 0
0AB4: marker = var PLUT_MARK
if
    not marker == 0
jf @GetPlutoniumStart
0AB3: var PLUT_MARK = 0
04C4: create_coordinate px py pz from_actor $PLAYER_ACTOR offset 0.0 0.0 1.0
0209: marker = random_int_in_ranges 60 100
04D3: get_nearest_car_path_coords_from px py pz type marker store_to x y z
z += 0.5
pickup = Pickup.Create(PLUTONIUM_MODEL, 2, x, y, z)
02A8: marker = create_marker 31 at x y z
Model.Load(LYBIAN_MODEL)
while not Model.Available(LYBIAN_MODEL)
    wait 0
end
04D3: get_nearest_car_path_coords_from x y z type 2 store_to x y z
lybian = Car.Create(LYBIAN_MODEL, x, y, z)
020B: explode_car lybian


:GetPlutoniumPickup
wait 0
0AB4: result = var TRAVEL
if
    result == 1 // integer values
then
    Pickup.Destroy(pickup)
    Marker.Disable(marker)
    Car.RemoveReferences(lybian)
    jump @GetPlutoniumStart
end
if
    Pickup.Picked_up(pickup)
jf @GetPlutoniumPickup

0164: disable_marker marker
Car.RemoveReferences(lybian)
stream_custom_script "Pickups/PlutoniumText.s"
3F84: play_sound "mission/notification.wav" loop 0
0AB3: var PLUTONIUM = 12
0AB3: var REFUELED = 0

:GetPlutoniumWaitRefuel
wait 0
0AB4: result = var TRAVEL
if
    result == 0 // integer values
jf @GetPlutoniumStart
0AB4: result = var REFUELED
if
    result == 1
jf @GetPlutoniumWaitRefuel

0506: vehicle_model LYBIAN_MODEL set_next_variation -1 -1
Model.Load(LYBIAN_MODEL)
Model.Load(#CLA)
Model.Load(#CLB)
Model.Load(#RUGER)
Model.Load(#MP5LNG)

:GetPlutoniumLoadModels
wait 0
if and
   Model.Available(LYBIAN_MODEL)
   Model.Available(#CLA)
   Model.Available(#CLB)
   Model.Available(#RUGER)
   Model.Available(#MP5LNG)
jf @GetPlutoniumLoadModels

// Create Car
04C4: create_coordinate px py pz from_actor $PLAYER_ACTOR offset 0.0 0.0 1.0
04D3: get_nearest_car_path_coords_from px py pz type 20 store_to x y z
03D3: point x y z get_nearby_vector x y z angle
lybian = Car.Create(LYBIAN_MODEL, x, y, z)
Car.Angle(lybian) = angle
0229: set_car lybian color_to 7 1

// DEBUG
0186: marker = create_marker_above_car lybian

// Create Driver and Passenger
0129: driver = create_actor 10 #CLA in_car lybian driverseat

enrage = 0
timera = 0

// Set driver flags
01ED: reset_actor driver flags
011A: set_actor driver flags 1
0243: set_actor driver ped_stats_to 16
03FE: set_actor driver money 200
01B2: give_actor driver weapon 25 ammo 30000 // Load the weapon model before using this

// Lock doors
020A: set_car lybian door_status_to 2

// Avoid obstacles
Car.SetMaxSpeed(lybian, 35.0)
00AF: set_car lybian driver_behaviour_to 5
00AE: set_vehicle lybian traffic_behavior_to 2

// Give gunner gun
009A: gunner = create_actor_pedtype 10 model #CLB at x y z
0489: set_actor gunner muted 0
011C: actor gunner clear_objective
0464: put_actor gunner into_turret_on_car lybian at_car_offset 0.0 0.0 1.0 position 0 180.0 with_weapon 27
01B2: give_actor gunner weapon 27 ammo 30000 // Load the weapon model before using this
011A: set_actor gunner flags 1
02E2: set_actor gunner weapon_accuracy_to 75
01CA: actor gunner kill_player $PLAYER_CHAR
0319: set_actor gunner wander_state_to 1

// Unload models
Model.Destroy(LYBIAN_MODEL)
Model.Destroy(#CLA)
Model.Destroy(#CLB)
Model.Destroy(#RUGER)
Model.Destroy(#MP5LNG)

:GetPlutoniumLybianChase
wait 0
if and
    not Actor.Dead($PLAYER_ACTOR)
    not Car.Wrecked(lybian)
jf @GetPlutoniumLybianRemove
0AB4: result = var TRAVEL
if
    result == 1 // integer values
then
    0AB4: result = var MODE
    if
        result == 1
    then
        Actor.DestroyInstantly(driver)
        Actor.DestroyInstantly(gunner)
        Car.Destroy(lybian)
        jump @GetPlutoniumStart
    end
    wait 1000
    lybian_health = Car.Health(lybian)
    0407: create_coordinate x y z from_car lybian offset 0.0 0.0 0.0

    jump @GetPlutoniumLybianTimeTravel
end

// Enrage after 25 seconds
if and
    enrage == 0
    timera >= 25000
then
    enrage = 1
    Car.SetMaxSpeed(lybian, 50.0)
end
02E3: result = car lybian speed

if and
    result <= 15.0
    gunner_mode == 0
    enrage == 1
    timera > 2000
then
    gunner_mode = 1
    cleo_call @SwitchWeapon 2 gunner #ROCKETLA
else if and
    result > 15.0
    gunner_mode == 1
    timera > 2000
then
    gunner_mode = 0
    cleo_call @SwitchWeapon 2 gunner #RUGER
end
end

jump @GetPlutoniumLybianChase

:GetPlutoniumLybianTimeTravel
wait 0
if
    Car.Wrecked(lybian)
then
    Camera.PointAt(x, y, z, 2)
    jump @GetPlutoniumLybianRemove
end
Camera.OnVehicle(lybian, 15, 2)
0407: create_coordinate x y z from_car lybian offset 0.0 0.0 0.0
if
   not fading
jf @GetPlutoniumLybianRemove
if
8185:   not car lybian health >= lybian_health
jf @GetPlutoniumLybianTimeTravel
Camera.PointAt(x, y, z, 2)
020B: explode_car lybian
3F84: play_sound "mission/missioncomplete.wav" loop 0
while not fading
    wait 0
end

:GetPlutoniumLybianRemove
Actor.RemoveReferences(driver)
Actor.RemoveReferences(gunner)
Car.RemoveReferences(lybian)
jump @GetPlutoniumStart

:SwitchWeapon
if
    not Actor.Dead(0@)
then
    if
        1@ == #ROCKETLA
    then
        2@ = 100
        3@ = 30
        4@ = 50
    else
        1@ = #RUGER
        2@ = 75
        3@ = 27
        4@ = 30000
    end
    Model.Load(1@)
    while not Model.Available(1@)
        wait 0
    end
    // Make it so they can't blow themselves up
    02E2: set_actor 0@ weapon_accuracy_to 2@
    01B2: give_actor 0@ weapon 3@ ammo 4@
    Model.Destroy(1@)
    timera = 0
end
cleo_return 0