{$CLEO .s}
{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE ../../Common/CustomOpcodes.txt}
{$USE RESTORATION}
const
    KEY_PICKUP_DROP = 9 //"TAB" Pickup/ Drop
    KEY_TAKE_PLUTONIUM = 6 //"AIM" Take Plutonium
    PLUTONIUM_WEAPON_BOX_MODEL = 121
    PLUTONIUM_WEAPON_CANISTER_MODEL = 122
end
    

0000:
thread 'PLBOX'

int vehicle
int open
int Spawned_Vehicle_Box
int Spawned_Weapon_Box
int plutonium_count
int plutonium_backup
int Sound_Can_Play = 1
int hastaken
int marker
int start
int frame 
float x
float y
float z
float zangle

while true
    wait 0
    0AB4: plutonium_count = var PLUTONIUM
gosub @Give_Plutonium_Count 
gosub @Give_Plutonium_Box
gosub @Drop_Plutonium_Box    
gosub @Pickup_Plutonium_Box
gosub @take
gosub @no_plutonium
gosub @not_holding_plutonium 
gosub @holding_plutonium_core 
gosub @ForceWeaponNoScroll
gosub @plutonium_core_removed

  
end
return

:Give_Plutonium_Count
if
start == 0
then 
plutonium_backup = 12 
start = 1
end
return

:Give_Plutonium_Box
if and
Spawned_Vehicle_Box == 0
Spawned_Weapon_Box == 0
start == 1
then
        Model.Load(0)
        038B: load_requested_models
        while not  Model.Available(0)
            wait 0
        end  
        01B2: give_actor $PLAYER_ACTOR weapon PLUTONIUM_WEAPON_BOX_MODEL ammo 1 
        wait 10 
        Spawned_Weapon_Box = 1                      
        Model.Destroy(PLUTONIUM_WEAPON_BOX_MODEL)
        stream_custom_script "Pickups/Plutonium/PlutoniumBoxWeaponAnims.s"  
        stream_custom_script "Pickups/Plutonium/PlutoniumBoxDisableWeapons.s" 
        stream_custom_script "Pickups/Plutonium/PlutoniumBoxCheatHandler.s"
        start = 2 
        end
return

:Drop_Plutonium_Box  
if and 
0AB0:   is_key_pressed KEY_PICKUP_DROP 
        Spawned_Vehicle_Box == 0
        Spawned_Weapon_Box == 1
02D7:   player $PLAYER_CHAR current_weapon == PLUTONIUM_WEAPON_BOX_MODEL
then
0459: terminate_all_scripts_with_this_name 'PLBOXC'
0459: terminate_all_scripts_with_this_name 'PLBOXW'
0459: terminate_all_scripts_with_this_name 'PLBOXWA' 
        0555: remove_actor $PLAYER_ACTOR weapon PLUTONIUM_WEAPON_BOX_MODEL
0A8C: write_memory 0x4BDC11 size 1 value 0x50 virtual_protect 1
0A8C: write_memory 0x4BDC12 size 1 value 0x6A virtual_protect 1
0A8C: write_memory 0x4BDC14 size 1 value 0xE8 virtual_protect 1 

        Model.Load(6052)
        038B: load_requested_models
        
        while not  Model.Available(6052)
            wait 0
        end 
                
        04C4: create_coordinate x y z from_actor $PLAYER_ACTOR offset 0.0 2.0 -1.0
        0172: zangle = actor $PLAYER_ACTOR z_angle
        vehicle = Car.Create(6052, x, y, z)   
0175: set_car vehicle z_angle_to zangle      
02AC: set_car  vehicle immunities  1  1  1  1  1
053F: set_car  vehicle tires_vulnerable  0
03ED: set_car vehicle not_damaged_when_upside_down 1  
020A: set_car vehicle door_status_to 2  
02A8: marker = create_marker 31 at x y z
Spawned_Weapon_Box = 0
Spawned_Vehicle_Box = 1
    wait 10                       
        Model.Destroy(6052)  
        end     
return
    
:Pickup_Plutonium_Box
if
Spawned_Vehicle_Box == 1 
then
00AA: store_car vehicle position_to x y z 

if and
Spawned_Weapon_Box == 0    
open == 0  
hastaken == 0
0AB0:   is_key_pressed KEY_PICKUP_DROP  
82D7:   not player $PLAYER_CHAR current_weapon == PLUTONIUM_WEAPON_CANISTER_MODEL
0102:   actor $PLAYER_ACTOR stopped_near_point_on_foot x y z radius 1.8 1.8 2.0 sphere 0
then  
0164: disable_marker marker
01C3: mark_car_as_no_longer_needed vehicle    
00A6: destroy_car vehicle                       
Model.Destroy(PLUTONIUM_WEAPON_BOX_MODEL)
        Spawned_Weapon_Box = 0
        Spawned_Vehicle_Box = 0
        start = 0  
        end
        end
return


:Take
if
Spawned_Vehicle_Box == 1 
then
00AA: store_car vehicle position_to x y z 
if and
Spawned_Weapon_Box == 0 
open == 0  
plutonium_count == 0
00E1:   player 0 pressed_button KEY_TAKE_PLUTONIUM // "AIM"
82D7:   not player $PLAYER_CHAR current_weapon == PLUTONIUM_WEAPON_CANISTER_MODEL
0102:   actor $PLAYER_ACTOR stopped_near_point_on_foot x y z radius 1.8 1.8 2.0 sphere 0
then
    
    0AB3: var PLUTONIUM = plutonium_backup
    plutonium_count = plutonium_backup
gosub @Check_Plutonium
gosub @Open_Anim
3F84: play_sound "delorean/geiger.wav" loop 1
wait 500
    //lift 
for x = 0.0 to 0.2 step 0.01
3F15: set_car vehicle component "plutcan" index plutonium_count pos 0.0 0.0 x
3F15: set_car vehicle component "plutcanliquid" index plutonium_count pos 0.0 0.0 x
3F15: set_car vehicle component "plutcaninterior" index plutonium_count pos 0.0 0.0 x
3F15: set_car vehicle component "plut" index plutonium_count pos 0.0 0.0 x 
    wait 0
    end    
wait 500
gosub @Give_Plutonium_Canister 
plutonium_backup = plutonium_count
plutonium_backup -= 1  
3F11: set_car vehicle component "plutcan" index plutonium_count visible 0
3F11: set_car vehicle component "plutcanliquid" index plutonium_count visible 0
3F11: set_car vehicle component "plutcaninterior" index plutonium_count visible 0
3F15: set_car vehicle component "plut" index plutonium_count pos 0.0 0.0 -1500.0   
wait 500
//close it    
gosub @Close_Anim
3F81: stop_sound "delorean/geiger.wav" 
wait 500
3F11: set_car vehicle component "plutcan" index plutonium_count visible 1
3F11: set_car vehicle component "plutcanliquid" index plutonium_count visible 1
3F11: set_car vehicle component "plutcaninterior" index plutonium_count visible 1 
3F15: set_car vehicle component "plutcan" index plutonium_count pos 0.0 0.0 0.0
3F15: set_car vehicle component "plutcanliquid" index plutonium_count pos 0.0 0.0 0.0
3F15: set_car vehicle component "plutcaninterior" index plutonium_count pos 0.0 0.0 0.0
3F15: set_car vehicle component "plut" index plutonium_count pos 0.0 0.0 0.0   
0AB3: var PLUTONIUM = 1
hastaken = 1 

        end
        end
return

:Give_Plutonium_Canister
        Model.Load(0)
        038B: load_requested_models
        while not  Model.Available(0)
            wait 0
        end  
        01B2: give_actor $PLAYER_ACTOR weapon PLUTONIUM_WEAPON_CANISTER_MODEL ammo 1               
        Model.Destroy(PLUTONIUM_WEAPON_CANISTER_MODEL)
    return

:Check_Plutonium
wait 0
//clear
for frame = 1 to 12
3F11: set_car vehicle component "plut" index frame visible 0
    end 
//actual check    
for frame = plutonium_count downto 0
3F11: set_car vehicle component "plut" index frame visible 1
    end     
return

:not_holding_plutonium
if 
plutonium_backup  == 0
then
hastaken = 0
end     
return

:no_plutonium
if and
Spawned_Vehicle_Box == 1
plutonium_backup  == 0
then
0164: disable_marker marker
01C3: mark_car_as_no_longer_needed vehicle    
00A6: destroy_car vehicle 
Spawned_Vehicle_Box = 0   
end     
return


:ForceWeaponNoScroll
if and
Spawned_Weapon_Box == 1
0490:   player $PLAYER_CHAR has_weapon PLUTONIUM_WEAPON_BOX_MODEL
then
01B8: set_player $PLAYER_CHAR armed_weapon_to PLUTONIUM_WEAPON_BOX_MODEL
end
return 

:holding_plutonium_core  
if and
hastaken == 1
0490:   player $PLAYER_CHAR has_weapon PLUTONIUM_WEAPON_CANISTER_MODEL
Sound_Can_Play == 1
then
3F84: play_sound "delorean/geiger.wav" loop 1
Sound_Can_Play = 2
end
return 

:plutonium_core_removed  
if and
82D7:   not player $PLAYER_CHAR current_weapon == PLUTONIUM_WEAPON_CANISTER_MODEL
Sound_Can_Play == 2
then
3F81: stop_sound "delorean/geiger.wav" 
Sound_Can_Play = 1
end
return 

    
//Anims Go Here
    :Open_Anim
stream_custom_script "Pickups/Plutonium/PlutoniumText.s" 
    for x = 0.0 downto -88.0 step 4.0
3F16: set_car vehicle component "pluttop" angle x 0.0 0.0 
    wait 0
    end 
    open = 1
    return
    
    :Close_Anim
    for x = -88.0 to 0.0 step 4.0
3F16: set_car vehicle component "pluttop" angle x 0.0 0.0 
    wait 0
    end 
    open = 0
    return 