{$CLEO .cs}
0000:
thread 'MAP'

{$INCLUDE Common/CleoConstants.txt}
int local_mall_sign
int local_PINE1
int local_PINE2
int current_date
int paper
int paper2  
int paper3 
//int vehicle

:DateCheckStart
//03C1: vehicle = player $PLAYER_CHAR car
stream_custom_script "map/mallclock.s" //clock
stream_custom_script "map/docdoor.s"
0AB3: current_date = var CDATE 
0AB4: local_mall_sign = var mall_sign  
0AB3: var mall_sign = 2 //- Twin Pine
while current_date == -1
    wait 10
end 
3F09: remove_build "1885"
3F09: remove_build "1955"
3F09: remove_build "1985"
3F09: remove_build "2015"
3F38: remove_objects "1885"
3F38: remove_objects "1955"
3F38: remove_objects "1985"
3F38: remove_objects "2015"
gosub @MallSignDestroy  


:DateCheck
//gosub @reset_trash
0AB4: current_date = var CDATE
if
    current_date < 19100101
then
    3F08: add_build "1885"
    3F37: add_objects "1885"  
    gosub @1885_News   
    01EB: set_traffic_density_multiplier_to 0.0
    jump @WaitToLeave1885
else if
    current_date < 19800101
then
    3F08: add_build "1955"
    3F37: add_objects "1955"
    stream_custom_script "Map/Clocktower_News.s" 19551113 19800101 
    gosub @TwinpinesTreesCreate 
    stream_custom_script "map/TwinPinesRipple.s" 
    stream_custom_script "map/barnprops.s" 19100101 19800101  
    01EB: set_traffic_density_multiplier_to 0.5   
    jump @WaitToLeave1955
else if
    current_date < 20100101
then
    3F08: add_build "1985"
    3F37: add_objects "1985"    
    gosub @1985_News
    gosub @MallSignCreate
    01EB: set_traffic_density_multiplier_to 0.7
    jump @WaitToLeave1985
else
    3F08: add_build "2015"
    3F37: add_objects "2015"
    gosub @2015_News
    gosub @MallSignCreate
    01EB: set_traffic_density_multiplier_to 0.9
    jump @WaitToLeave2015
end
end
end

:WaitToLeave1885
0AB4: current_date = var CDATE
wait 0
if
    current_date >= 19100101
else_jump @WaitToLeave1885
3F09: remove_build "1885"
3F38: remove_objects "1885" 
jump @DateCheck

:WaitToLeave1955
0AB4: current_date = var CDATE
wait 0
if or
    current_date < 19100101
    current_date >= 19800101 // We traveled to the present/future
else_jump @WaitToLeave1955
3F09: remove_build "1955"
3F38: remove_objects "1955" 
end_thread_named 'CTNEWS' 
gosub @TwinPinesTreesDestroy
jump @DateCheck

:WaitToLeave1985
0AB4: current_date = var CDATE
wait 0
if or
  current_date < 19800101 // We traveled back to the past
  current_date >= 20100101 // We traveled to the future
else_jump @WaitToLeave1985
3F09: remove_build "1985"
3F38: remove_objects "1985" 
gosub @MallSignDestroy
jump @DateCheck

:WaitToLeave2015
0AB4: current_date = var CDATE
wait 0
if
  current_date < 20100101 // We traveled to the past/present
else_jump @WaitToLeave2015
3F09: remove_build "2015"
3F38: remove_objects "2015"  
gosub @MallSignDestroy
jump @DateCheck

:TwinPinesTreesCreate  
local_PINE1 = Object.Init(5322,  -518.2, 276.72, 13.07)
local_PINE2 = Object.Init(5322,  -519.33, 276.92, 13.07)
0AB3: var PINE1 = local_PINE1
0AB3: var PINE2 = local_PINE2
Object.Angle(local_PINE1) = 80.0
Object.Angle(local_PINE2) = -100.0
return

:TwinPinesTreesDestroy
end_thread_named 'pinechk'
end_thread_named 'ufonew' 
0108: destroy_object local_PINE1
0108: destroy_object local_PINE2 
return


:MallSignCreate 
0AB4: local_mall_sign = var mall_sign
if
    local_mall_sign == 0
then          
    3F08: add_build "nopine"
else if
    local_mall_sign == 1
then
    3F08: add_build "lonepine"
else  
    3F08: add_build "twinpine"
end
end
return

:MallSignDestroy
3F09: remove_build "nopine"
3F09: remove_build "lonepine"
3F09: remove_build "twinpine"
return      


:1985_News
0209: paper = random_int_in_ranges 1 6 //1 = blank 2 = blue 3 = white
3F0A: car 0@ replace_tex "particle" "newspaper01_64" with "newspaper01_64" index paper
0209: paper2 = random_int_in_ranges 1 6 //1 = blank 2 = pink 3 = yellow
3F0A: car 0@ replace_tex "particle" "gameleaf01_64" with "gameleaf01_64" index paper2
0209: paper3 = random_int_in_ranges 4 6 // 4 = george 6 = doc
3F0A: car 0@ replace_tex "particle" "gameleaf02_64" with "gameleaf02_64" index paper3
return



:2015_News
3F0A: car 0@ replace_tex "particle" "newspaper01_64" with "newspaper01_64" index 1
3F0A: car 0@ replace_tex "particle" "gameleaf01_64" with "gameleaf01_64" index 1
0209: paper = random_int_in_ranges 2 4 //1 = blank 2 = youth 3 = gang
3F0A: car 0@ replace_tex "particle" "gameleaf02_64" with "gameleaf02_64" index paper
return

:1885_News
3F0A: car 0@ replace_tex "particle" "newspaper01_64" with "newspaper01_64" index 9
3F0A: car 0@ replace_tex "particle" "gameleaf01_64" with "gameleaf01_64" index 0
3F0A: car 0@ replace_tex "particle" "gameleaf02_64" with "gameleaf02_64" index 0
return



 