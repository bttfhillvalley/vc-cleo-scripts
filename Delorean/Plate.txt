{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
float rz
float x
float y
float z
float px
float py
float pz
float dist
int plate
float anim
int set_time_var
int ignore
3F62: plate = car vehicle create_component "plate"
01BB: store_object plate position_to x y z
if
    01F3:   car vehicle airborne
then
    Object.CollisionDetection(plate) = True
    Object.ToggleInMovingList(plate) = True
    jump @PlateDestroy
end
02CE: z = ground_z x y z
01BC: put_object plate at x y z
Object.CollisionDetection(plate) = False
Object.ToggleInMovingList(plate) = False
3F95: ignore = play_sound "delorean/plate.wav" pos x y z loop 0 min 5.0

// Spin plate
for anim = 0 to 28 step 1
    wait 10
    if
        rz < -360.0 // floating-point values
    then
        rz += 360.0 // floating-point values
    end
    0453: object plate set_rotation 0.0 -65.0 rz
    if
        anim < 20
    then
        rz -= 45.0
    else
        rz -= 22.5  // Slow down spin near the end
    end
end

// Plate falls
for anim = 0.0 downto -90.0 step 10.0
    wait 10
    0453: object plate set_rotation anim -65.0 rz
end

// Play sound
3F95: ignore = play_sound "delorean/plate_fall.wav" pos x y z loop 0 min 5.0

:PlateDestroy
wait 10
04C4: create_coordinate px py pz from_actor $PLAYER_ACTOR offset 0.0 1.2 0.0
0509: dist = distance between point x y and point px py
0AB4: set_time_var = var SET_TIME
if or
    set_time_var == 1
    dist > 100.0 // floating-point values
jf @PlateDestroy
if
    03CA:   object plate exists
then
    Object.Destroy(plate)
end
terminate_this_custom_script
