{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
int vehicle
int vehicle_flags
float x
float y
float z
float rf
float rb
float lf
float lb
float drf
float drb
float dlf
float dlb
float steering
float extension
float damper
const
   WHEEL_FRONT_OFFSET =  -0.280126
   WHEEL_BACK_OFFSET = -0.304341
end
0000:
while true
    wait 0
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end
    if or
        Actor.InCar($PLAYER_ACTOR, vehicle)
        3F2A: is_player_in_remote_mode_with_car vehicle
    then
        if
            timerb > 100
        then
            0AB4: vehicle_flags = var TM_FLAGS
        end
    else
        timerb = 0
    end
    0B10: extension = vehicle_flags AND FLYING_BIT
    if
        extension == 0
    then
        3F0E: store_car vehicle wheel_rotation lf lb rf rb
        3F0F: store_car vehicle wheel_speed dlf dlb drf drb
    else

        cleo_call @SlowWheel 2 dlf lf ret dlf lf
        cleo_call @SlowWheel 2 dlb lb ret dlb lb
        cleo_call @SlowWheel 2 drf rf ret drf rf
        cleo_call @SlowWheel 2 drb rb ret drb rb

        3F1E: set_car vehicle wheel_rotation lf lb rf rb
        3F1F: set_car vehicle wheel_speed dlf dlb drf drb
    end

    // Get extension of struts
    3F1B: get_car vehicle component "strutrb" pos extension y z
    damper = extension
    damper /= -0.3
    damper += 1.0

    // Set position first, which includes extension and suspension.
    3F1B: get_car vehicle component "wheel_rf_dummy" pos x y z
    z -= WHEEL_FRONT_OFFSET
    006B: z *= damper
    3F14: set_car vehicle component "strutrf" pos extension 0 z

    3F1B: get_car vehicle component "wheel_rb_dummy" pos x y z
    z -= WHEEL_FRONT_OFFSET
    006B: z *= damper
    3F14: set_car vehicle component "strutrb" pos extension 0 z

    extension *= -1.0

    3F1B: get_car vehicle component "wheel_lf_dummy" pos x y z
    z -= WHEEL_BACK_OFFSET
    006B: z *= damper
    3F14: set_car vehicle component "strutlf" pos extension 0 z

    3F1B: get_car vehicle component "wheel_lb_dummy" pos x y z
    z -= WHEEL_BACK_OFFSET
    006B: z *= damper
    3F14: set_car vehicle component "strutlb" pos extension 0 z

    // Set angles
    3F1C: get_car vehicle angle "fxwheelrf_" pos x y z
    3F36: get_car vehicle steering_angle steering
    steering *= 0.6
    006B: steering *= damper
    3F16: set_car vehicle component "holderlf"                 angle 0 y steering
    3F16: set_car vehicle component "brakelf"                  angle 0 y steering
    3F16: set_car vehicle component "fxrotorlf"                angle lf 0 steering
    3F16: set_car vehicle component "fxwheellfrimbttf1_"       angle lf 0 steering
    3F16: set_car vehicle component "fxwheellfrimbttf3_"       angle lf 0 steering
    3F16: set_car vehicle component "fxwheellfrimbttf3rr_"     angle lf 0 steering

    3F16: set_car vehicle component "fxrotorlb"                angle lb 0 0
    3F16: set_car vehicle component "fxwheellbrimbttf1_"       angle lb 0 0
    3F16: set_car vehicle component "fxwheellbrimbttf3_"       angle lb 0 0
    3F16: set_car vehicle component "fxwheellbrimbttf3rr_"     angle lb 0 0
    y *= -1.0
    3F16: set_car vehicle component "holderrf"                 angle 0 y steering
    3F16: set_car vehicle component "brakerf"                  angle 0 y steering
    3F16: set_car vehicle component "fxrotorrf"                angle rf 0 steering
    3F16: set_car vehicle component "fxwheelrfrimbttf1_"       angle rf 0 steering
    3F16: set_car vehicle component "fxwheelrfrimbttf3_"       angle rf 0 steering
    3F16: set_car vehicle component "fxwheelrfrimbttf3rr_"     angle rf 0 steering

    3F16: set_car vehicle component "fxrotorrb"                angle rb 0 0
    3F16: set_car vehicle component "fxwheelrbrimbttf1_"       angle rb 0 0
    3F16: set_car vehicle component "fxwheelrbrimbttf3_"       angle rb 0 0
    3F16: set_car vehicle component "fxwheelrbrimbttf3rr_"     angle rb 0 0

    cleo_call @DamagedWheels 2 vehicle vehicle_flags
end

:SlowWheel
if
    0@ >= 0.05
then
    0@ -= 0.1
else if
    0@ <= -0.05
then
    0@ += 0.1
else
    0@ = 0.0
end
end
0087: 2@ = 0@
2@ *= 180.0
2@ /= 3.14159625
005B: 1@ += 2@
cleo_return 2 0@ 1@

:DamagedWheels
3F27: get_car vehicle wheel_status_to 3@
0B10: 6@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
0B10: 5@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
0B10: 4@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
if
    3@ == 2
then
    cleo_return 0
end

3@ *= -1
3@ += 1
4@ *= -1
4@ += 1
5@ *= -1
5@ += 1
6@ *= -1
6@ += 1

if
    3F40:   is_car vehicle component "fxwheelbttf1lf" visible
then
3F10: set_car vehicle component "fxtirebttf1lf" visible 3@
3F10: set_car vehicle component "fxtirebttf1lb" visible 4@
3F10: set_car vehicle component "fxtirebttf1rf" visible 5@
3F10: set_car vehicle component "fxtirebttf1rb" visible 6@
else if
    3F40:   is_car vehicle component "fxwheelbttf3lf" visible
then
    3F10: set_car vehicle component "fxtirebttf3lf" visible 3@
    3F10: set_car vehicle component "fxtirebttf3lb" visible 4@
    3F10: set_car vehicle component "fxtirebttf3rf" visible 5@
    3F10: set_car vehicle component "fxtirebttf3rb" visible 6@
end
end
cleo_return 0
