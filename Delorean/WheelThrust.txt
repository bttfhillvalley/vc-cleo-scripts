{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
float z_offset
float amount
float vehicle_speed
float x
float y
float z
float dx
float dy
float dz
float jx
float jy
float jz
int engine_state
int ignore

:WheelThrust
wait 0
3F02: engine_state = car vehicle engine_status
if and
    not Car.Wrecked(vehicle)
    not engine_state == 0
jf @WheelThrustEnd
if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
jf @WheelThrustEnd
// Limited amount of time to engage wheel thrusters after hover conversion
if
   timera < 500 // integer values
jf @WheelThrustEnd
if
00E1:   key_pressed 0 16
jf @WheelThrust
timera = 0 // integer values
3F96: ignore = attach_sound "delorean/wheel_thrust.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0

// Fade in
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
cleo_call @ThrusterParticle 3 vehicle -1.0 0.75
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
cleo_call @ThrusterParticle 3 vehicle -1.0 0.75
cleo_call @ThrusterParticle 3 vehicle -1.25 0.70

:WheelThrustCheck
wait 10
// Only thrust for 2 seconds max
3F1C: get_car vehicle component "fxwheelrf_" rotation x y z
0097: abs y
if and
    not Car.Wrecked(vehicle)
    y > 89.0
jf @WheelThrustFadeOut
if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
jf @WheelThrustFadeOut
if
   timera < 2000 // integer values
jf @WheelThrustFadeOut

// Unload if we let go of the gas
if
00E1:   key_pressed 0 16
jf @WheelThrustFadeOut

// Vent thruster will override the wheel thruster
if
80E1:   not key_pressed 0 6
jf @WheelThrustFadeOut
3F10: set_car vehicle component "fxthrusterbttf2rbth" visible 1
3F10: set_car vehicle component "fxthrusterbttf2rfth" visible 1
3F10: set_car vehicle component "fxthrusterbttf2lbth" visible 1
3F10: set_car vehicle component "fxthrusterbttf2lfth" visible 1

// Thrusters
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
cleo_call @ThrusterParticle 3 vehicle -1.0 0.75
cleo_call @ThrusterParticle 3 vehicle -1.25 0.70
cleo_call @ThrusterParticle 3 vehicle -1.5 0.65
jump @WheelThrustCheck

:ThrusterParticle
gosub @CalculateSpeedOffset
0407: create_coordinate x y z from_car vehicle offset -1.17951 -1.296 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA RGB 255 200 175 255 lifespan 10
0407: create_coordinate x y z from_car vehicle offset -1.17951 1.353 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA RGB 255 200 175 255 lifespan 10
0407: create_coordinate x y z from_car vehicle offset 1.17951 -1.296 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA RGB 255 200 175 255 lifespan 10
0407: create_coordinate x y z from_car vehicle offset 1.17951 1.353 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA RGB 255 200 175 255 lifespan 10
cleo_return 0

:CalculateSpeedOffset
3F33: get_car vehicle velocity_direction dx dy dz
02E3: vehicle_speed = car vehicle speed
vehicle_speed /= 30.0
006B: dx *= vehicle_speed
006B: dy *= vehicle_speed
006B: dz *= vehicle_speed
return

:AddSpeedOffset
005B: x += dx
005B: y += dy
005B: z += dz
0208: jx = random_float -0.05 0.05
0208: jy = random_float -0.05 0.05
0208: jz = random_float -0.05 0.05
005B: x += jx
005B: y += jy
005B: z += jz
return

:WheelThrustFadeOut
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
cleo_call @ThrusterParticle 3 vehicle -1.0 0.75
cleo_call @ThrusterParticle 3 vehicle -1.25 0.70
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
cleo_call @ThrusterParticle 3 vehicle -1.0 0.75
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75
cleo_call @ThrusterParticle 3 vehicle -0.75 0.75
wait 10
cleo_call @ThrusterParticle 3 vehicle -0.5 0.75

:WheelThrustEnd
3F10: set_car vehicle component "fxthrusterbttf2rbth" visible 0
3F10: set_car vehicle component "fxthrusterbttf2rfth" visible 0
3F10: set_car vehicle component "fxthrusterbttf2lbth" visible 0
3F10: set_car vehicle component "fxthrusterbttf2lfth" visible 0
terminate_this_custom_script
