{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int rear_deck
int travel_var
float rx
float ry
float rz
float x
float y
float z
float vehicle_speed
int delay = 50
int times = 0
int ice = 255
3F86: attach_sound "delorean/cold.wav" to_car vehicle offset 0 0 0 loop 1 min 10.0
0B10: rear_deck = vehicle_flags AND REAR_BIT  // Get car variation
gosub @SetFrost

:ColdStart
wait 10
if
    ice > 0
else_jump @ColdWisps
0AB4: travel_var = var TRAVEL
if
    travel_var == 0 // integer values
else_jump @ColdEnd
if
   not Car.Wrecked(vehicle)
else_jump @ColdEnd
if
    timera > 195
then
    timera = 0
    ice -= 1
    gosub @SetFrost
end
if
    001D: timerb > delay
then
    timerb = 0 // integer values
    times += 1 // integer values
    if
      times == 2 // integer values
    then
        delay += 1 // integer values
        times = 0 // integer values
    end

    // Effects
    gosub @ColdSteamParticle
    gosub @ColdSteamParticle

    // Plutonium version gets extra effects
    if
        rear_deck == REAR_REACTOR_BIT
    then
        gosub @ColdSteamParticle
        gosub @ColdSteamParticle
        gosub @ColdSteamParticle
    end
end
jump @ColdStart

:ColdSteamParticle
0208: rx = random_float -1.2 1.2
0208: ry = random_float -2.5 2.5
0208: rz = random_float 0.0 0.65
0407: create_coordinate x y z from_car vehicle offset rx ry rz
3F60: scatter_particle 22 0.2 at x y z 0 0 -0.05 RGBA 100 100 100 128 lifespan 0
return

:ColdWisps
// This is when we have less cold effects and it stops
delay = 0 // integer values
timera = 0 // integer values

:ColdWispsStart
wait 0
if
   not Car.Wrecked(vehicle)
else_jump @ColdEnd
if
   delay < 5 // integer values
else_jump @ColdEnd
02E3: vehicle_speed = car vehicle speed
if
  vehicle_speed < 10.0
jf @ColdWispsIntermittent
gosub @ColdSteamParticle
gosub @ColdSteamParticle

:ColdWispsIntermittent
wait 10
if
   not Car.Wrecked(vehicle)
else_jump @ColdEnd
if
  timera > 499 // integer values
else_jump @ColdWispsIntermittent
delay += 1 // integer values
timera = 0 // integer values
jump @ColdWispsStart

:ColdEnd
3F91: stop_sound "delorean/cold.wav" index vehicle
terminate_this_custom_script

:SetFrost
3F12: set car vehicle component "roof_fr" alpha ice
3F12: set car vehicle component "door_lf_hi_fr" alpha ice
3F12: set car vehicle component "door_lf_hi_window_fr" alpha ice
3F12: set car vehicle component "door_rf_hi_fr" alpha ice
3F12: set car vehicle component "door_rf_hi_window_fr" alpha ice
3F12: set car vehicle component "wing_lr_hi_fr" alpha ice
3F12: set car vehicle component "wing_rr_hi_fr" alpha ice
3F12: set car vehicle component "wing_lf_hi_fr" alpha ice
3F12: set car vehicle component "wing_rf_hi_fr" alpha ice
3F12: set car vehicle component "windscreen_hi_fr" alpha ice
3F12: set car vehicle component "chassis_hi_fr" alpha ice
3F12: set car vehicle component "bonnet_hi_fr" alpha ice
3F12: set car vehicle component "vents_fr" alpha ice
if and
    3F50:   not is_car vehicle component "bttf1" visible
    3F50:   not is_car vehicle component "bttf2" visible
then
    3F10: set_car vehicle component "vents_fr" visible 0
end
return
