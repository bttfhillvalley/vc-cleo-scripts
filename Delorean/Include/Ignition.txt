:Ignition
{$INCLUDE Variables.txt}
if
    Car.wrecked(vehicle)
then
    return
end
3F02: var1 = car vehicle engine_status
0AB4: var2 = var HUD_DISP
0AB4: var3 = var ENGINE_TURNOVER
0AB4: var4 = var REMOTE_TRAVEL
// Handle Ignition
if
    80E1:   not key_pressed 0 16  // Accelerate Key
then
    accel_key = 0
end

if and
    var1 == 0
    8119:   not car vehicle wrecked
    00DC:   player $PLAYER_CHAR driving vehicle
    00E1:   key_pressed 0 16  // Accelerate Key
    accel_key == 0
then
    0209: var5 = random_int 1 100
    3F16: rotate_car_part "ignitionkey" angle 135.0 0.0 0.0 car vehicle
    3F16: rotate_car_part "ignition" angle 135.0 0.0 0.0 car vehicle
    if or
        var3 == 0
        var5 == 50 // integer values
    then
        3F03: car vehicle turn_on_engine
        var3 = 0
        var1 = 1
    else
        if
            3F93: is_sound_stopped "engine_turnover.wav" index vehicle
        then
            3F86: attach_sound "engine_turnover.wav" to_car vehicle offset 0 0 0 loop 1 min 10.0
        end
    end
else
    if
        3F92: is_sound_playing "engine_turnover.wav" index vehicle
    then
        3F91: stop_sound "engine_turnover.wav" index vehicle
    end
end

// Handle shutting down when leaving car
if and
    var1 == 1
    var4 == 0
    8119:   not car vehicle wrecked
    00DC:   player $PLAYER_CHAR driving vehicle
then
    if
        00E1:   key_pressed 0 15  // Exit car key
    then
        02D4: car vehicle turn_off_engine
        var1 = 0
    end
end

// Sounds and setting engine state
if
    engine_state == 0
then
    if
        var1 == 1
    then
        engine_state = 1 // Engine on
        0AB3: var ENGINE_TURNOVER = 0
        3F86: attach_sound "engine_start.wav" to_car vehicle offset 0 0 0 loop 0 min 10.0
    end
else
    if and
        8119:   not car vehicle wrecked
        00DC:   player $PLAYER_CHAR driving vehicle
        00E1:   key_pressed 0 16  // Accelerate Key
    then
        accel_key = 1
    end

    if
        var1 == 0
    then
        engine_state = 0
        // Don't play shutdown if the vehicle is time travelling
        if
            var4 == 0
        then
            3F86: attach_sound "engine_stop.wav" to_car vehicle offset 0 0 0 loop 0 min 10.0
        end
    end
end
return
