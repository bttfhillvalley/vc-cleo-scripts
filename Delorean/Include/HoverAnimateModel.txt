:HoverAnimateModel
// Move struts
// Shocks are at a 45 degree angle so they have a weird movement,  They need to be adjusted in both the X and Z axis.
02F7: extension_angle = sinus 45.0 // cosine swapped with sinus
extension_angle *= extension

// will adjust Z. It is always negative.
shock_pos = extension_angle // Shock
shock_pos *= -1.0

// is for the piston extension
piston_pos = shock_pos // Piston

// controls how far the piston extends based on wheel rotation.
piston_ext = pivot // Piston
piston_ext *= 0.001

// Extend/retract piston
piston_pos -= piston_ext

// TEMP
3F14: set_car vehicle component "fxthrusterbttf2lfon" pos 0.03 0.0 0.0
3F14: set_car vehicle component "fxthrusterbttf2lbon" pos 0.04 0.0 0.0
3F14: set_car vehicle component "fxthrusterbttf2rfon" pos -0.03 0.0 0.0
3F14: set_car vehicle component "fxthrusterbttf2rbon" pos -0.04 0.0 0.0

// Extend
3F14: set_car vehicle component "strutrb"         pos extension 0.0 0.0
3F14: set_car vehicle component "brakerb"         pos extension 0.0 0.0
3F14: set_car vehicle component "holderrb"        pos extension 0.0 0.0
3F14: set_car vehicle component "shockrb"         pos extension_angle 0.0 shock_pos
3F14: set_car vehicle component "shockpistonrb"   pos extension_angle 0.0 piston_pos
3F14: set_car vehicle component "fxwheelrb_"      pos extension 0.0 0.0

3F14: set_car vehicle component "strutrf"         pos extension 0.0 0.0
3F14: set_car vehicle component "brakerf"         pos extension 0.0 0.0
3F14: set_car vehicle component "holderrf"        pos extension 0.0 0.0
3F14: set_car vehicle component "shockrf"         pos extension_angle 0.0 shock_pos
3F14: set_car vehicle component "shockpistonrf"   pos extension_angle 0.0 piston_pos
3F14: set_car vehicle component "fxwheelrf_"      pos extension 0.0 0.0
extension *= -1.0
extension_angle *= -1.0
3F14: set_car vehicle component "strutlb"         pos extension 0.0 0.0
3F14: set_car vehicle component "brakelb"         pos extension 0.0 0.0
3F14: set_car vehicle component "holderlb"        pos extension 0.0 0.0
3F14: set_car vehicle component "shocklb"         pos extension_angle 0.0 shock_pos
3F14: set_car vehicle component "shockpistonlb"   pos extension_angle 0.0 piston_pos
3F14: set_car vehicle component "fxwheellb_"      pos extension 0.0 0.0

3F14: set_car vehicle component "strutlf"         pos extension 0.0 0.0
3F14: set_car vehicle component "brakelf"         pos extension 0.0 0.0
3F14: set_car vehicle component "holderlf"        pos extension 0.0 0.0
3F14: set_car vehicle component "shocklf"         pos extension_angle 0.0 shock_pos
3F14: set_car vehicle component "shockpistonlf"   pos extension_angle 0.0 piston_pos
3F14: set_car vehicle component "fxwheellf_"      pos extension 0.0 0.0
extension *= -1.0

// Rotate wheels
3F16: set_car vehicle component "holderrf"        angle 0.0 pivot 0.0
3F16: set_car vehicle component "holderrb"        angle 0.0 pivot 0.0
3F16: set_car vehicle component "brakerf"         angle 0.0 pivot 0.0
3F16: set_car vehicle component "brakerb"         angle 0.0 pivot 0.0
3F16: set_car vehicle component "fxwheelrf_"      angle 0.0 pivot 0.0
3F16: set_car vehicle component "fxwheelrb_"      angle 0.0 pivot 0.0
pivot *= -1.0
3F16: set_car vehicle component "holderlf"        angle 0.0 pivot 0.0
3F16: set_car vehicle component "holderlb"        angle 0.0 pivot 0.0
3F16: set_car vehicle component "brakelf"         angle 0.0 pivot 0.0
3F16: set_car vehicle component "brakelb"         angle 0.0 pivot 0.0
3F16: set_car vehicle component "fxwheellf_"      angle 0.0 pivot 0.0
3F16: set_car vehicle component "fxwheellb_"      angle 0.0 pivot 0.0
pivot *= -1.0

cleo_call @RotateWheels 3 vehicle vehicle_flags extension
return

:RotateWheels
// enum {
//    CARWHEEL_FRONT_LEFT,
//    CARWHEEL_REAR_LEFT,
//    CARWHEEL_FRONT_RIGHT,
//    CARWHEEL_REAR_RIGHT
// };

// Damaged Wheel
cleo_call @DamagedWheels 3 vehicle vehicle_flags 2@ ret 3@ 4@ 5@ 6@ 7@ 8@ 9@ 10@
3F0E: store_car vehicle wheel_rotation 11@ 12@ 13@ 14@
2@ /= -0.3
2@ += 1.0
3F36: get_car vehicle steering_angle 15@
15@ *= 0.6
006B: 15@ *= 2@
005B: 3@ += 15@

3F16: set_car vehicle component "fxwheellf"                angle 11@ 7@ 3@
3F16: set_car vehicle component "fxwheellfrimbttf1_"       angle 11@ 7@ 3@
3F16: set_car vehicle component "fxwheellfrimbttf3_"       angle 11@ 7@ 3@
3F16: set_car vehicle component "fxwheellfrimbttf3rr_"     angle 11@ 7@ 3@

3F16: set_car vehicle component "fxwheellb"                angle 12@ 8@ 4@
3F16: set_car vehicle component "fxwheellbrimbttf1_"       angle 12@ 8@ 4@
3F16: set_car vehicle component "fxwheellbrimbttf3_"       angle 12@ 8@ 4@
3F16: set_car vehicle component "fxwheellbrimbttf3rr_"     angle 12@ 8@ 4@

3F36: get_car vehicle steering_angle 15@
006B: 15@ *= 2@
if
    15@ > 0.0
then
    15@ *= 0.6
end
005B: 5@ += 15@
3F16: set_car vehicle component "fxwheelrf"            angle 13@ 9@ 5@
3F16: set_car vehicle component "fxwheelrfrimbttf1_"   angle 13@ 9@ 5@
3F16: set_car vehicle component "fxwheelrfrimbttf3_"   angle 13@ 9@ 5@
3F16: set_car vehicle component "fxwheelrfrimbttf3rr_" angle 13@ 9@ 5@

3F16: set_car vehicle component "fxwheelrb"            angle 14@ 10@ 6@
3F16: set_car vehicle component "fxwheelrbrimbttf1_"   angle 14@ 10@ 6@
3F16: set_car vehicle component "fxwheelrbrimbttf3_"   angle 14@ 10@ 6@
3F16: set_car vehicle component "fxwheelrbrimbttf3rr_" angle 14@ 10@ 6@
cleo_return 0

:DamagedWheels
2@ /= 0.3


3F0E: store_car vehicle wheel_rotation 11@ 12@ 13@ 14@
0B10: 3@ = vehicle_flags AND WHEEL_STATUS_BIT
0B15: 3@ = 3@ >> WHEEL_STATUS_SHIFT
0B10: 6@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
0B10: 5@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
0B10: 4@ = 3@ AND 0x3
0B15: 3@ = 3@ >> 2
cleo_call @DamagedWheelCosAngle 2 3@ 11@ ret 7@
cleo_call @DamagedWheelCosAngle 2 4@ 12@ ret 8@
cleo_call @DamagedWheelCosAngle 2 5@ 13@ ret 9@
cleo_call @DamagedWheelCosAngle 2 6@ 14@ ret 10@

cleo_call @DamagedWheelSinAngle 2 3@ 11@ ret 3@
cleo_call @DamagedWheelSinAngle 2 4@ 12@ ret 4@
cleo_call @DamagedWheelSinAngle 2 5@ 13@ ret 5@
cleo_call @DamagedWheelSinAngle 2 6@ 14@ ret 6@
3@ *= -1.0
4@ *= -1.0
7@ *= -1.0
8@ *= -1.0
006B: 7@ *= 2@
006B: 8@ *= 2@
006B: 9@ *= 2@
006B: 10@ *= 2@
cleo_return 8 3@ 4@ 5@ 6@ 7@ 8@ 9@ 10@

:DamagedWheelSinAngle
if
    0@ == 1
then
    02F6: 1@ = sine 1@
    1@ *= 17.1887
    cleo_return 1 1@
end
cleo_return 1 0.0

:DamagedWheelCosAngle
if
    0@ == 1
then
    02F7: 1@ = cosine 1@
    1@ *= 5.0
    cleo_return 1 1@
end
cleo_return 1 0.0