{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE ../../Time/Include/Variables.txt}

:SidAnimation
for column = 1 to 10
    cleo_call @SidLine 3 vehicle vehicle_flags column
end
cleo_return 0

:SidLine
02E3: vehicle_speed = car vehicle speed
// Set target
0B10: var2 = vehicle_flags AND TC_FRIED_BIT
0B10: var3 = vehicle_flags AND TC_BIT
0B10: var4 = vehicle_flags AND COOLDOWN_BIT
if
    3F50:   not is_car vehicle component "sid" visible
then
    target = 0
else if or
    not var2 == 0 // Fried time circuits
    var3 == 0 // Time Circuits Off
    not var4 == 0 // Cooldown
then
    target = 0
else if
    vehicle_speed >= 46.5 // 85.3
then
    target = 20
else if
    vehicle_speed >= 43.5
then
    if
        column == 3
    then
        target = 13
    else if
        column == 8
    then
        target = 10
    else if
        column == 10
    then
        target = 18
    else
        target = 20
    end
    end
    end
else if
    vehicle_speed >= 40.5 // 74.3 mph, subject to change
then
    if
        column == 1
    then
        target = 19
    else if
        column == 3
    then
        target = 6
    else if or
        column == 5
        column == 8
    then
        target = 0
    else if
        column == 6
    then
        target = 12
    else if
        column == 9
    then
        target = 20
    else
        target = 10
    end
    end
    end
    end
    end
else
    02E3: target = car vehicle speed
    target *= 1.835
    if
        column == 1
    then
        target /= 5.0
    else if
        column == 3
    then
        target /= 16.0
    else if or
        column == 5
        column == 8
    then
        target = 0.0
    else if
        column == 6
    then
        target /= 8.0
    else if
        column == 9
    then
        target /= 4.0
    else
        target /= 3.0
        if
            target > 10.0
        then
            target = 10.0
        end
    end
    end
    end
    end
    end
    008C: target = float_to_integer target
end
end
end
end
end

// Get which LED is currently visible
for current = 20 downto 1 step 1
    gosub @GetLed
    if
        3F41:   is_car vehicle component "sidledsline" index led visible
    then
        break
    end
end

// Set target for SID
if
    001D: target > current
then
    current += 1
    gosub @GetLed
    3F11: set_car vehicle component "sidledsline" index led visible 1
else if
    001D: current > target
then
    3F11: set_car vehicle component "sidledsline" index led visible 0
end
end
cleo_return 0

:GetLed
0085: led = current
if
    column == 10
then
    led *= 10
end
led *= 10
005A: led += column
return

