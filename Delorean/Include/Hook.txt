{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE ../../Time/Include/Variables.txt}
:Hook
if
    not Actor.InCar($PLAYER_ACTOR, vehicle)
else_jump @HookReturn

0B10: var1 = vehicle_flags AND HOOK_BIT

// Hook check
if or
    var1 == HOOK_SIDE_BIT
    var1 == HOOK_UP_BIT
else_jump @HookReturn
0407: create_coordinate x y z from_car vehicle offset 1.5 -0.5 0.0

if and
    var1 == HOOK_SIDE_BIT
    00FF:   actor $PLAYER_ACTOR 0 x y z radius 1.0 1.0 2.0
then
    020A: set_car vehicle door_status_to 2
else
    020A: set_car vehicle door_status_to 0
end

3F06: var2 = get_car vehicle door DOOR_FRONT_RIGHT status
if or
    var1 == HOOK_SIDE_BIT
    var2 == 1  // Closed
else_jump @HookReturn

if
    00E1:   key_pressed 0 4
else_jump @HookReturn

// Put hook up or down
0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
if
    var1 == HOOK_SIDE_BIT
then
    0B11: vehicle_flags = vehicle_flags OR HOOK_UP_BIT
else
    0B11: vehicle_flags = vehicle_flags OR HOOK_SIDE_BIT
end
stream_custom_script "Delorean/HookAnimate.s" vehicle vehicle_flags
wait 250

:HookReturn
cleo_return 1 vehicle_flags

:HookRemove
0B10: var1 = vehicle_flags AND HOOK_BIT  // Get car variation
if or
    var1 == HOOK_SIDE_BIT
    var1 == HOOK_UP_BIT
then
    // Remove hook
    0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
    0B11: vehicle_flags = vehicle_flags OR HOOK_HOLDER_BIT
end
cleo_return 1 vehicle_flags

:HookDetach
0B10: var1 = vehicle_flags AND HOOK_BIT  // Get car variation
if
    var1 == HOOK_SIDE_BIT
then
    0407: create_coordinate x y z from_car vehicle offset 1.0 5.0 -1.0
    0174: rz = car vehicle z_angle
    rx = -90.0
    stream_custom_script "Delorean/HookDetach.s" vehicle x y z rz rx
else if
    var1 == HOOK_UP_BIT
then
    0407: create_coordinate x y z from_car vehicle offset 0.0 0.0 0.75
    0174: rz = car vehicle z_angle
    rx = 30.0
    stream_custom_script "Delorean/HookDetach.s" vehicle x y z rz rx
end
end
cleo_return 0