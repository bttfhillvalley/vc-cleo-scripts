{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE ../../Time/Include/Variables.txt}

:Coil
// Hoodbox effects
0B10: var1 = vehicle_flags AND HOOD_BIT // Hoodbox variation
if
   not var1 == 0  // Has Hoodbox
then
    0087: var2 = vehicle_speed
    var2 /= 30.0 // floating-point values (never used in VC or GTA 3)
    0208: y = random_float 1.425 1.825
    0208: x = random_float -0.2 0.2
    005D: y += var2 // floating-point values (never used in VC or GTA 3)
    0407: create_coordinate x y z from_car vehicle offset x y 0.35
    0437: scatter_particle 2 0.1 at x y z 0 0 0
    0208: y = random_float 1.425 1.825
    0208: x = random_float -0.2 0.2
    005D: y += var2 // floating-point values (never used in VC or GTA 3)
    0407: create_coordinate x y z from_car vehicle offset x y 0.35
    gosub @AddSpeedOffset
    0437: scatter_particle 11 0.1 at x y z 0 0 0
end
return

:CoilOn
02E3: vehicle_speed = car vehicle speed
0B10: var1 = vehicle_flags AND COOLDOWN_BIT
0AB4: var2 = var TIME_MACHINE
if
    var1 == 0
then
    0087: var1 = vehicle_speed
    var1 -= 40.5
    var1 *= 64.0
    008C: var1 = float_to_integer var1
else
    if
        timerb < 1000
    then
        var1 = 255
    else
        var1 = 0
    end
end
if
    003B: vehicle == var2
then
    3F52: fade_car vehicle component "fluxcoilson" alpha var1 step 10
else
    if
        var1 > 0
    then
        3F10: set_car vehicle component "fluxcoilson" visible 1
    else
        3F10: set_car vehicle component "fluxcoilson" visible 0
    end
end
cleo_return 0

:CoilOff
0AB4: var2 = var TIME_MACHINE
if
    003B: vehicle == var2
then
    3F52: fade_car vehicle component "fluxcoilson" alpha 0 step 10
else
    3F10: set_car vehicle component "fluxcoilson" visible 0
end
cleo_return 0
