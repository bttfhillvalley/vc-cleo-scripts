{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int engine
int turnover
int wiper_state // 0 - off, 1 - intermittent, 2 - normal speed, 3 - fast speed
float stalk_state = 0.0 // 5.0 - single, 0.0 - off, -5.0 - normal, -10.0 - fast
int wiper_direction
int key_held
float stalk_angle = 0.0
float wiper_angle = 0.0
float passenger_wiper_angle
float driver_angle
float passenger_angle
float speed

while true
    wait 10
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end
    3F02: engine = car vehicle engine_state
    0AB4: turnover = var ENGINE_TURNOVER

    if and
        00DC:   player $PLAYER_CHAR in_car vehicle
        3F93: is_sound_stopped "engine_turnover.wav" index vehicle
        turnover == 1
    then
        engine = 1
    end

    // Key held logic
    if and
        not is_key_pressed 0x49
        not is_key_pressed 0x55
    then
        key_held = 0
    end

    // Reset stalk if in momentary position
    if and
        not is_key_pressed 0x49
        stalk_state == 5.0
    then
        stalk_state = 0.0
    end

    // Wiper control
    if and
        00DC:   player $PLAYER_CHAR in_car vehicle
        is_key_pressed 0x55 // U - Counterclockwise
        stalk_state > -10.0
        key_held == 0
    then
        stalk_state -= 5.0
        key_held = 1
    else if and
        00DC:   player $PLAYER_CHAR in_car vehicle
        is_key_pressed 0x49 // I - Clockwise
        stalk_state < 5.0
        key_held == 0
    then
        stalk_state += 5.0
        key_held = 1
        // Single tap resets wipers
        if
            stalk_state == 5.0
        then
            wiper_state = 0
            timera = 0
            if
            wiper_direction == 0
            then
                wiper_direction = 1
            end
        end
    end
    end

    // Animate stalk
    if
    0025: stalk_state > stalk_angle
    then
        stalk_angle += 1.0
    else if
        0025: stalk_angle > stalk_state
    then
        stalk_angle -= 1.0
    end
    end
    3F16: rotate_car_part "wiperslever" angle 0.0 stalk_angle 0.0 car vehicle

    // Turn on wipers
    if and
        stalk_state < 0.0
        wiper_direction == 0
    then
        wiper_direction = 1
    end

    // Set speed
    if
        stalk_state == -10.0
    then
        speed = 6.0
    else
        speed = 3.0
    end

    // Set wiper state
    if and
        stalk_state == 0.0
        not wiper_state == 1
    then
        wiper_state = 0
    else if
        stalk_state == -5.0
    then
        wiper_state = 2 // Normal
    else if
        stalk_state == -10.0
    then
        wiper_state = 3
    end
    end
    end

    // Animate wipers
    if and
        wiper_direction == 1
        not engine == 0
    then
        // Stop wipers if wipers are off

        // Intermittent, don't animate yet
        if and
            wiper_state == 1
            timera < 8000
        then
            if
                wiper_state == 0
            then
                wiper_direction = 0
            end
            continue
        end

        if
            3F93: is_sound_stopped "wiper_up.wav" index vehicle
        then
            3F86: attach_sound "wiper_up.wav" to_car vehicle offset 0.0 0.88 0.24 loop 0 size 2.0
        end
        wiper_angle -= speed
        if
            wiper_angle < -65.0
        then
            wiper_angle = -65.0
            wiper_direction = -1
        end
    else if and
        wiper_direction == -1
        not engine == 0
    then
        if
            3F93: is_sound_stopped "wiper_down.wav" index vehicle
        then
            3F86: attach_sound "wiper_down.wav" to_car vehicle offset 0.0 0.88 0.24 loop 0 size 2.0
        end
        wiper_angle += speed
        if
            wiper_angle > 0.0
        then
            wiper_angle = 0.0
            timera = 0

            // Check to see if stalk is held down
            if
                stalk_state == 5.0
            then
                wiper_state = 1
            end

            // Disable animations if wipers are off
            if
                wiper_state == 0
            then
                wiper_direction = 0
            else
                wiper_direction = 1
            end

        end
    end
    end
    passenger_wiper_angle = wiper_angle
    //passenger_wiper_angle *= 1.25 // Add this back in when scaling is fixed.
    driver_angle = wiper_angle
    driver_angle /= 5.5
    passenger_angle = passenger_wiper_angle
    passenger_angle /= 12.5
    3F16: rotate_car_part "wiperdriver" angle 0.0  driver_angle wiper_angle car vehicle
    3F16: rotate_car_part "wiperpassenger" angle 0.0 passenger_angle passenger_wiper_angle  car vehicle */
end
