{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
int vehicle
int vehicle_flags
int bit_check
float x
float y
float z
float rx
float ry
float rz

0000:
0B10: bit_check = vehicle_flags AND HOOK_BIT

if
    bit_check == HOOK_UP_BIT
then
    gosub @HookUp
else
    gosub @HookDown
end

terminate_this_custom_script

:HookUp
3F1C: get_car vehicle component "hookguidebttf1_" rotation rx ry rz

while rx > 0.1
    wait 0
    rx -= 15.0
    3F16: set_car vehicle component "hookguidebttf1_" rotation rx 0.0 0.0
end
3F16: set_car vehicle component "hookguidebttf1_" rotation 0.0 0.0 0.0

// Raise hook
3F1B: get_car vehicle component "hookbttf1_" pos x y z
while z < 0.846
    wait 0
    z += 0.053
    3F14: set_car vehicle component "hookbttf1_" position x y z
end
z = 0.846424


// Rotate and move hook
3F1C: get_car vehicle component "hookbttf1_" rotation rx ry rz
while rx < 300.863157
    wait 0
    rx += 5.0

    if
        ry < -0.005
    then
        ry += 0.01
    else
        ry = 0.0
    end

    if
        rz < -0.1
    then
        rz += 0.43
    else
        rz = 0.0
    end

    if
        x > 0.01
    then
        x -= 0.05
    else
        x = 0.0
    end

    if
        y < -1.368
    then
        y += 0.01
    else
        y = -1.3675
    end
    3F14: set_car vehicle component "hookbttf1_" position x y z
    3F16: set_car vehicle component "hookbttf1_" rotation rx ry rz
end
3F14: set_car vehicle component "hookbttf1_" position 0.0 -1.3675 0.846424
3F16: set_car vehicle component "hookbttf1_" rotation 300.863157 0.0 0.0


// Insert hook
3F1B: get_car vehicle component "hookbttf1" pos x y z
while y < 0.25
    wait 0
    y += 0.05
    3F14: set_car vehicle component "hookbttf1" position 0.0 y 0.0
end
3F14: set_car vehicle component "hookbttf1" position 0.0 0.25 0.0
3F10: set_car vehicle component "hookcablesonbttf1" visible 1
3F10: set_car vehicle component "hookcablesoffbttf1" visible 0
3F10: set_car vehicle component "hookcableplugbttf1" visible 0
return

:HookDown
// Remove hook
3F10: set_car vehicle component "hookcablesonbttf1" visible 0
3F10: set_car vehicle component "hookcablesoffbttf1" visible 1
3F10: set_car vehicle component "hookcableplugbttf1" visible 1
3F1B: get_car vehicle component "hookbttf1" pos x y z
while y > 0.0
    wait 0
    y -= 0.05
    3F14: set_car vehicle component "hookbttf1" position 0.0 y 0.0
end
3F14: set_car vehicle component "hookbttf1" position 0.0 0.0 0.0

// Rotate Hook
3F1B: get_car vehicle component "hookbttf1_" pos x y z

3F1C: get_car vehicle component "hookbttf1_" rotation rx ry rz
rx += 360.0
while rx > 178.7
    wait 0
    rx -= 5.0

    if
        ry > -0.04
    then
        ry -= 0.01
    else
        ry = -0.04
    end

    if
        rz > -1.72
    then
        rz -= 0.43
    else
        rz = -1.72
    end

    if
        x < 0.922
    then
        x += 0.05
    else
        x = 0.922921
    end

    if
        y > -1.46645
    then
        y -= 0.01
    else
        y = -1.46645
    end
    3F14: set_car vehicle component "hookbttf1_" position x y z
    3F16: set_car vehicle component "hookbttf1_" rotation rx ry rz
end
3F14: set_car vehicle component "hookbttf1_" position 0.922921 -1.46645 z
3F16: set_car vehicle component "hookbttf1_" rotation 178.7 -0.04 -1.72

// Lower hook
while z > 0.422
    wait 0
    z -= 0.053
    3F14: set_car vehicle component "hookbttf1_" position x y z
end
3F14: set_car vehicle component "hookbttf1_" position 0.922921 -1.46645 0.422211

3F1C: get_car vehicle component "hookguidebttf1_" rotation rx ry rz
while rx < 89.4
    wait 0
    rx += 15.0
    3F16: set_car vehicle component "hookguidebttf1_" rotation rx 0.0 0.0
end
3F16: set_car vehicle component "hookguidebttf1_" rotation 89.5 0.0 0.0
return