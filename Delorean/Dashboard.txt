{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int engine
int prev_engine
int current_gear
float vehicle_speed
float target
float temperature
float oil_needle
float temp_needle
float volt_needle
float fuel_needle
float ignition
int turnover
int current_time
float var1
float var2

volt_needle = -20.0
temperature = 75.0

:Dashboard
wait 10
if
    Car.Wrecked(vehicle)
then
    terminate_this_custom_script
end
if
    timera > 600000
then
    timera = 600000
end
3F02: engine = car vehicle engine_state
3F04: current_gear = get_car vehicle gear
02E3: vehicle_speed = car vehicle speed
0AB4: turnover = var ENGINE_TURNOVER
0AB4: current_time = var CTIME

// Reset timer to initiate startup/shutdown sequences if engine state changes
if
    not engine == prev_engine
then
    prev_engine = engine
    if
        not engine == 0
    then
        timera = 0
    end
end

// Set targets
if
    not engine == 0
then

    // Temperature
    if
        temperature < 160.0
    then
        temperature += 0.05
    end

    // Voltage gauge
    if
        timera < 500
    then
        // Have a power dip when the car starts up
        cleo_call @Animate 4 volt_needle 5.0 1.0 1.0 result volt_needle
        cleo_call @Animate 4 ignition -60.0 15.0 15.0 result ignition
    else
        cleo_call @Animate 4 volt_needle 40.0 1.0 1.0 result volt_needle
        cleo_call @Animate 4 ignition -30.0 15.0 15.0 result ignition
    end

    // Oil temperature gauge
    gosub @Temperature

    // Oil gauge
    3F66: get_joypad 0 throttle_to var1 var2
    if
        current_gear == 0
    then
        var1 = var2
    end
    var2 = var1
    008D: var1 = integer_to_float var1
    008D: var2 = integer_to_float var2

    // Idle -20, Full throttle -60
    var1 /= 255.0
    var1 *= -40.0
    var1 -= 20.0

    // Oil pressure delta speed
    var2 /= 255.0
    var2 *= 2.0
    var2 += 1.0

    // Temperature effects pressure
    if
        temperature > 140.0
    then
        target = temperature
        target *= 2.0
        target -= 340.0
    else if or
        temperature > 100.0
        timera > 1000
    then
        target = -60.0
    else
        target = 0.0
    end
    end

    if
        var1 < target
    then
        target = var1
    end
    cleo_call @Animate 4 oil_needle target var2 1.0 result oil_needle

    // Fuel needle
    cleo_call @Animate 4 fuel_needle -30.0 1.0 1.0 result fuel_needle

    // Lights
    gosub @Lights
else
    // Cool down oil
    if
        temperature > 75.0
    then
        temperature -= 0.01
    end

    if
        turnover == 1
    then
        if
            3F92: is_sound_playing "delorean/engine_turnover.wav" index vehicle
        then
            cleo_call @Animate 4 volt_needle 10.0 1.0 1.0 result volt_needle
            cleo_call @Animate 4 ignition -60.0 15.0 15.0 result ignition
            timera = 0
        else
            cleo_call @Animate 4 volt_needle 15.0 1.0 1.0 result volt_needle
            cleo_call @Animate 4 ignition -30.0 15.0 15.0 result ignition
        end
        gosub @Temperature
        cleo_call @Animate 4 oil_needle 0.0 1.0 1.0 result oil_needle
        gosub @Lights
    else
        // Lights!

        if
            Actor.InCar($PLAYER_ACTOR, vehicle)
        then
            cleo_call @Animate 4 volt_needle 15.0 1.0 1.0 result volt_needle
            cleo_call @Animate 4 ignition 0.0 15.0 15.0 result ignition
        else
            cleo_call @Animate 4 volt_needle -20.0 1.0 1.0 result volt_needle
            cleo_call @Animate 4 ignition 60.0 15.0 15.0 result ignition
        end
        3F10: set_car vehicle component "oillight" visible 0
        3F10: set_car vehicle component "batterylight" visible 0
        3F10: set_car vehicle component "seatbeltlight" visible 0
        3F10: set_car vehicle component "lowbeamslight" visible 0
    end
    gosub @BatteryLight
end
gosub @LambdaLight

// Move needles
3F16: set_car vehicle component "voltsneedle" angle 0.0 volt_needle 0.0
3F16: set_car vehicle component "oilneedle" angle 0.0 oil_needle 0.0
3F16: set_car vehicle component "tempneedle" angle 0.0 temp_needle 0.0
3F16: set_car vehicle component "fuelneedle" angle 0.0 fuel_needle 0.0
3F16: set_car vehicle component "ignitionkey" angle ignition 0.0 0.0
3F16: set_car vehicle component "ignition" angle ignition 0.0 0.0
jump @Dashboard

:Animate
if
    0025: 0@ > 1@
then
    0063: 0@ -= 2@
    if
        0025: 1@ > 0@
    then
        0087: 0@ = 1@
    end
else
    005B: 0@ += 3@
    if
        0025: 0@ > 1@
    then
        0087: 0@ = 1@
    end
end
cleo_return 1 0@

:Lights
if
    oil_needle > -10.0
then
    3F10: set_car vehicle component "oillight" visible 1
else
    3F10: set_car vehicle component "oillight" visible 0
end
if
    volt_needle < 20.0
then
    3F10: set_car vehicle component "batterylight" visible 1

else
    3F10: set_car vehicle component "batterylight" visible 0
end
if or
    timera < 5000
    3F92: is_sound_playing "delorean/engine_turnover.wav" index vehicle
then
    3F10: set_car vehicle component "seatbeltlight" visible 1
else
    3F10: set_car vehicle component "seatbeltlight" visible 0
end
if
    fuel_needle > -5.0
then
    3F10: set_car vehicle component "fuellight" visible 1
else
    3F10: set_car vehicle component "fuellight" visible 0
end

3F9D: var1 = get_car vehicle light_status
3F10: set_car vehicle component "lowbeamslight" visible var1
return

:BatteryLight
0AB4: var1 = var HUD_DISP
0AB4: var2 = var IN_GARAGE

if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    engine == 0
    var1 == 1
    var2 == 0
then
    if
        3F93: is_sound_stopped "delorean/engine_turnover.wav" index vehicle
    then
        cleo_call @DrawTexture 9 3 783.0 155.0 45.0 23.0 255 255 255 255
    else
        cleo_call @DrawTexture 9 3 783.0 155.0 45.0 23.0 64 64 64 255 // battery light off
    end
end
return

:LambdaLight
engine = Car.Health(vehicle)
if
    engine < 390
then
    3F10: set_car vehicle component "lambdalight" visible 1
    0AB4: var1 = var HUD_DISP
    0AB4: var2 = var IN_GARAGE
    if and
        Actor.InCar($PLAYER_ACTOR, vehicle)
        var1 == 1
        var2 == 0
    then
        cleo_call @DrawTexture 9 4 738.0 155.0 45.0 23.0 255 255 255 255
    end
else
    3F10: set_car vehicle component "lambdalight" visible 0
end
return

:Temperature
target = temperature
target *= 0.35
target -= 45.0
if
    target < -10.0
then
    target = -10.0
end
cleo_call @Animate 4 temp_needle target 1.0 1.0 result temp_needle
return

{$INCLUDE ../Common/DrawTexture.txt}
