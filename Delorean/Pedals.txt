{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
float vehicle_speed
int current_gear
int prev_gear
int hovering
float gas
float brake
float clutch
float gas_angle
float brake_angle
float clutch_angle

timera = 1000
while true
    wait 0
    if
        Car.wrecked(vehicle)
    then
        terminate_this_custom_script
    end
    02E3: vehicle_speed = car vehicle speed
    3F04: current_gear = get_car vehicle gear
    3F27: get_car vehicle wheel_status_to hovering
    //01E5: text_1number_highpriority 'SPEED' hovering time 10 1  // Cost: $~1~

    if
        not Actor.InCar($PLAYER_ACTOR, vehicle)
    then
        continue
    end

    // Handle Gas and Brake Pedals
    if
        current_gear == 0 // Reversing
    then
        // Reverse
        3F66: get_joypad 0 throttle_to brake gas
    else
        3F66: get_joypad 0 throttle_to gas brake
    end
    008D: gas = integer_to_float gas
    008D: brake = integer_to_float brake
    gas /= 255.0
    gas *= 20.0
    brake /= 255.0
    brake *= 20.0

    // Handle Clutch pedal
    if
        not prev_gear == current_gear
    then
        timera = 0
        prev_gear = current_gear
    end
    if and
        hovering == 0 // Driving
        timera < 100
    then
        gas = 0.0
        clutch = 20.0
    else
        clutch = 0.0
        timera = 1000
    end

    // Animate Pedals
    cleo_call @GetPedalDelta 2 gas_angle gas ret gas_angle
    cleo_call @GetPedalDelta 2 brake_angle brake ret brake_angle
    cleo_call @GetPedalDelta 2 clutch_angle clutch ret clutch_angle
    3F16: set_car vehicle component "gaspedal" angle gas_angle 0.0 0.0
    3F16: set_car vehicle component "brakepedal" angle brake_angle 0.0 0.0
    3F16: set_car vehicle component "clutchpedal" angle clutch_angle 0.0 0.0
end

:GetPedalDelta
0087: 2@ = 1@
0087: 2@ -= 0@
if
    2@ > 5.0
then
    2@ = 5.0
end
if
    2@ < -5.0
then
    2@ = 0.0
end
005B: 0@ += 2@
cleo_return 1 0@
