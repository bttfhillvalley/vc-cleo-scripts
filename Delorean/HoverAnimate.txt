{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int flying_flag
int travel_var
float vehicle_speed
float x
float y
float z
float floor
float extension
float pivot
float extension_angle
float shock_pos
float piston_pos
float piston_ext
int ignore

0B10: flying_flag = vehicle_flags AND FLYING_BIT   // Is car flying?
if
    flying_flag == 0
then
    extension = 0.3 // Wheel retracting
    pivot = 90.0 // Wheel rotating back
    0AB3: var LANDING = 1
    3F86: attach_sound "delorean/hover_retract.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
    cleo_call @CheckTimeMachine 1 vehicle ret ignore
    if
        ignore == true
    then
        3F86: attach_sound "delorean/landspeeder_decelerate_lower_pitch.wav" to_car vehicle pos 0.0 0.0 0.0 loop 0 size 10.0
        3F98: set_sound "delorean/landspeeder_decelerate_lower_pitch.wav" index vehicle volume 0.5
        3F86: attach_sound "delorean/landspeeder_decelerate_2_lower_pitch.wav" to_car vehicle pos 0.0 0.0 0.0 loop 0 size 10.0
        stream_custom_script "Delorean/HoverStop.s" vehicle
    end
else
    stream_custom_script "Delorean/Variation.s" vehicle vehicle_flags
    extension = 0.0 // Wheel extending
    pivot = 0.0 // Wheel rotating out
    3F2D: set_car vehicle engine_sound 0
    3F28: set_car vehicle wheel_status 2 // Remove tires
    3F86: attach_sound "delorean/hover_extend.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
    3F86: attach_sound "delorean/engine_idle.wav" to_car vehicle pos 0.0 0.0 0.0 loop 1 size 10.0
    cleo_call @CheckTimeMachine 1 vehicle ret ignore
    if
        ignore == true
    then
        stream_custom_script "Delorean/HoverStart.s" vehicle
    end
    3F26: set_car vehicle upward_force 50.0
end

:HoverAnimateStart
wait 10
0AB4: travel_var = var TRAVEL
if
    travel_var == 0
else_jump @HoverAnimateStart
if
    not Car.Wrecked(vehicle)
else_jump @HoverAnimateEnd
Car.StorePos(vehicle, x y z)
02CE: floor = ground_z x y z
0063: z -= floor // floating-point values (never used in VC or GTA 3)

// Animate
if
    flying_flag == 0 // Landing
then
    if
        3F3E: car vehicle wheels_on_ground
    then
        0AB3: var LANDING = 0
    end
    if
        pivot > 0.0
    then
        pivot -= 5.0
    else if
        extension > 0.0
    then
        extension -= 0.01
        3F28: set_car vehicle wheel_status 0  // Add tires back.  TODO: Keep track of tire status
    else
        extension = 0.0
        if
            3F3E: car vehicle wheels_on_ground
        then
            if or
                Actor.InCar($PLAYER_ACTOR, vehicle)
                3F2A:   is_player_in_remote_mode_with_car vehicle
            then
                3F2D: set_car vehicle engine_sound 1
            end
            3F91: stop_sound "delorean/engine_idle.wav" index vehicle
            3F91: stop_sound "delorean/landspeeder_loop_lower_pitch.wav" index vehicle
            stream_custom_script "Delorean/Variation.s" vehicle vehicle_flags
            jump @HoverAnimateEnd
        end
    end
    end
else  // Flying
    if
        extension < 0.3
    then
        extension += 0.01
    else if
        pivot < 90.0
    then
        pivot += 5.0
    else
        stream_custom_script "Delorean/WheelThrust.s" vehicle
        jump @HoverAnimateEnd
    end
    end
end
gosub @HoverAnimateModel
gosub @HoverSmoke
cleo_call @RotateWheels 2 vehicle extension
jump @HoverAnimateStart

:HoverAnimateEnd
0AB3: var CONVERSION = 0
0AB3: var LANDING = 0
terminate_this_custom_script

:HoverSmoke
// Effects
// Smoke effects on takeoff/landing
02E3: vehicle_speed = car vehicle speed
if and
    vehicle_speed < 5.0   // slow speeds only as well
    z < 5.0   // Only show smoke close to the ground.
then
    0407: create_coordinate x y z from_car vehicle offset 0.75 0.8 -0.425
    0437: scatter_particle 33 0.1 at x y z 0 0 -0.1
    0407: create_coordinate x y z from_car vehicle offset -0.75 0.8 -0.425
    0437: scatter_particle 33 0.1 at x y z 0 0 -0.1
    0407: create_coordinate x y z from_car vehicle offset 0.625 -0.6 -0.425
    0437: scatter_particle 33 0.1 at x y z 0 0 -0.1
    0407: create_coordinate x y z from_car vehicle offset -0.625 -0.6 -0.425
    0437: scatter_particle 33 0.1 at x y z 0 0 -0.1
end

{$INCLUDE Include/HoverAnimateModel.txt}
return

{$INCLUDE Include/TimeMachine.txt}
