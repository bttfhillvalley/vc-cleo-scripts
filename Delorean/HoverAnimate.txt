{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int flying_flag
int travel_var
float vehicle_speed
float x
float y
float z
float ground
float extension
float pivot
float piston_ext
float damper
int ignore

0B10: flying_flag = vehicle_flags AND FLYING_BIT   // Is car flying?
if
    flying_flag == FLYING_LANDING_BIT
then
    extension = 0.3 // Wheel retracting
    pivot = 90.0 // Wheel rotating back
    3F86: attach_sound "delorean/hover_retract.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
    if and
        3F92: is_sound_playing "delorean/landspeeder_loop_lower_pitch.wav" index vehicle
        3F93: is_sound_stopped "delorean/landspeeder_decelerate_lower_pitch.wav" index vehicle
    then
        cleo_call @CheckTimeMachine 1 vehicle ret ignore
        3F86: attach_sound "delorean/landspeeder_decelerate_lower_pitch.wav" to_car vehicle pos 0.0 0.0 0.0 loop 0 size 10.0
        3F98: set_sound "delorean/landspeeder_decelerate_lower_pitch.wav" index vehicle volume 0.5
        if
            ignore == true
        then
            3F86: attach_sound "delorean/landspeeder_decelerate_2_lower_pitch.wav" to_car vehicle pos 0.0 0.0 0.0 loop 0 size 10.0
        end
        stream_custom_script "Delorean/HoverStop.s" vehicle
    end
else
    extension = 0.0 // Wheel extending
    pivot = 0.0 // Wheel rotating out
    3F2D: set_car vehicle engine_sound 0
    3F28: set_car vehicle wheel_status HOVER_WHEELS
    03ED: set_car vehicle not_damaged_when_upside_down 1
    3F86: attach_sound "delorean/hover_extend.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
    3F02: ignore = car vehicle engine_status
    gosub @HoverSmoke
    if
        not ignore == 0
    then
        3F86: attach_sound "delorean/engine_idle.wav" to_car vehicle pos 0.0 0.0 0.0 loop 1 size 10.0
        stream_custom_script "Delorean/HoverStart.s" vehicle
        3F26: set_car vehicle upward_force 50.0
    end
end

:HoverAnimateStart
if
    not Car.Wrecked(vehicle)
else_jump @HoverAnimateEnd

// Don't animate if we're currently outside the space time continuum
0AB4: travel_var = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    not travel_var == 0
then
    jump @HoverAnimateStart
end

// Animate
if
    flying_flag == FLYING_LANDING_BIT
then
    if
        pivot > 0.0
    then
        pivot -= 5.0
    else if
        extension >= 0.01
    then
        extension -= 0.01
        cleo_call @HoverAddWheels 2 vehicle vehicle_flags
    else
        extension = 0.0
        cleo_call @HoverAddWheels 2 vehicle vehicle_flags
        0B10: vehicle_flags = vehicle_flags AND FLYING_BITMASK
        jump @HoverAnimateEnd
    end
    end
else  // Flying
    if
        extension < 0.3
    then
        extension += 0.01
    else if
        pivot < 90.0
    then
        pivot += 5.0
    else
        jump @HoverAnimateEnd
    end
    end
end
gosub @HoverAnimateModel
wait 10
jump @HoverAnimateStart

:HoverAnimateEnd
terminate_this_custom_script

:HoverAddWheels
3F27: get_car vehicle wheel_status_to ignore
if
    ignore == HOVER_WHEELS
then
    0B10: ignore = vehicle_flags AND WHEEL_STATUS_BIT
    0B15: ignore = ignore >> WHEEL_STATUS_SHIFT
    3F28: set_car vehicle wheel_status ignore  // Add tires back.
end
cleo_return 0

:HoverSmoke
// Check Speed
/*3F39: get_car vehicle velocity x y z
gosub @GetLength
if
    x > 0.02
then
    return
end

// Check angle
3F44: get_car vehicle up_vector x y z
gosub @GetLength
if
    x > 0.5
then
    return
end   */
Car.StorePos(vehicle, x, y, z)

0407: create_coordinate x y z from_car vehicle offset 0.75 0.8 -0.425
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset -0.75 0.8 -0.425
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset 0.625 -0.6 -0.425
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset -0.625 -0.6 -0.425
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z

0407: create_coordinate x y z from_car vehicle offset 0.795 1.35 -0.3
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset -0.795 1.35 -0.3
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset 0.795 -1.295 -0.3
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
0407: create_coordinate x y z from_car vehicle offset -0.795 -1.295 -0.3
stream_custom_script "Delorean/HoverSmoke.s" vehicle x y z
return

:GetLength
x *= x
y *= y
x += x
01FB: x = square_root x
return

{$INCLUDE Include/HoverAnimateModel.txt}
{$INCLUDE Include/TimeMachine.txt}
