{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
float geiger_angle = 10.0
float actual_geiger_angle
float target_geiger_angle
float power_angle = 0.0
float target_power_angle
int tcon_var
int next_tcon_var
int fuel_var
int rear_deck
int weather

0B10: tcon_var = vehicle_flags AND TC_BIT
0B10: fuel_var = vehicle_flags AND FUEL_BIT
next_tcon_var = tcon_var
timera = 5000
3F16: set_car vehicle component "pchamberrefneedle" angle 0.0 15.0 0.0

while true
    wait 10
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end
    if
        Actor.InCar($PLAYER_ACTOR, vehicle)
    then
        if
            timerb > 100
        then
            0AB4: vehicle_flags = var TM_FLAGS
        end
    else
        timerb = 0
    end
    0B10: tcon_var = vehicle_flags AND TC_BIT
    0B10: fuel_var = vehicle_flags AND FUEL_BIT
    0B10: rear_deck = vehicle_flags AND REAR_BIT
    0607: weather = get_current_weather

    // Handle time circuits turning on and off
    if
        not next_tcon_var == tcon_var
    then
        if
            next_tcon_var == 0
        then
            timera = 5000
        else
            timera = 0
        end
        tcon_var = next_tcon_var
    end

    // Handle animation
    if and
        timera > 2000
        timera < 3000
    then
        if
            not fuel_var == 0
        then
            3F84: play_sound "delorean/plut_gauge.wav" loop 0
        end
        timera = 5000
    end

    if
        timera > 3000
    then
        timera = 5000
    end

    if or
        tcon_var == 0
        timera < 2000
    then
        3F10: set_car vehicle component "gloveboxgaugeslights" visible 0
    else
        3F10: set_car vehicle component "gloveboxgaugeslights" visible 1
    end

    // Set target angle position
    if or
        fuel_var == 0
        tcon_var == 0
        timera < 2000
    then
        target_power_angle = 0.0
        target_geiger_angle = 0.0
    else
        target_power_angle = 23.0
        if
            rear_deck == 0
        then
            target_geiger_angle = 45.0
        else
            target_geiger_angle = 0.0
        end
    end

    // If in the nth Dimension, actually have the Geiger counter be a geiger counter
    if and
        not rear_deck == 0
        weather == 20
    then
        0208: target_geiger_angle = random_float 35.0 55.0
        if
            3F93: is_sound_stopped "delorean/geiger.wav" index vehicle
        then
            3F86: attach_sound "delorean/geiger.wav" to_car vehicle offset 0.62 0.527 0.176 loop 1 min 1.0
        end
    else
        if
            3F92: is_sound_playing "delorean/geiger.wav" index vehicle
        then
            3F91: stop_sound "delorean/geiger.wav" index vehicle
        end
    end

    // Animate Needle
    if
        power_angle < target_power_angle
    then
        power_angle += 1.0
    else if
        power_angle > target_power_angle
    then
        power_angle -= 1.0
    end
    end

    if
        geiger_angle < target_geiger_angle
    then
        geiger_angle += 1.0
    else if
        geiger_angle > target_geiger_angle
    then
        geiger_angle -= 1.0
    end
    end

    // Set Needle
    actual_geiger_angle = geiger_angle
    if
        actual_geiger_angle < 10.0
    then
        actual_geiger_angle = 10.0
    end
    3F16: set_car vehicle component "pchamberneedle" angle 0.0 actual_geiger_angle 0.0
    3F16: set_car vehicle component "ppowerneedle" angle 0.0 power_angle 0.0
    3F16: set_car vehicle component "primaryneedle" angle 0.0 power_angle 0.0
end