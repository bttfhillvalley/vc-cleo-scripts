{$CLEO .s}
{$INCLUDE ../../Common/CleoConstants.txt}
0000:
thread 'clock'
int num1
int num2
int hours
int hours1
int hours2
int min
int min1
int min2
while true
    wait 10
    00BF: hours = current_time_hours, min = current_time_minutes

    if
        hours < 12
    then
        3F0A: replace_tex "mall_sign" "tpc_am" with "mall_sign" "am0"
    else
        3F0A: replace_tex "mall_sign" "tpc_am" with "mall_sign" "pm0"
        hours -= 12
    end

    if
        hours == 0
    then
        hours = 12
    end

    cleo_call @NumToDigits 1 hours ret hours1 hours2
    cleo_call @NumToDigits 1 min ret min1 min2

    if
        hours1 == 0
    then
        3F0A: replace_tex "mall_sign" "tpc_hr" with "mall_sign" "blank0"
    else
        3F0A: replace_tex "mall_sign" "tpc_hr" with "mall_sign" "time1"
    end
    3F0B: replace_tex "mall_sign" "tpc_hr1" with "mall_sign" "time" index hours2
    3F0B: replace_tex "mall_sign" "tpc_m1" with "mall_sign" "time" index min1
    3F0B: replace_tex "mall_sign" "tpc_m2" with "mall_sign" "time" index min2
end

:NumToDigits
num2 = num1
num1 /= 10
num1 *= 10
num2 -= num1
num1 /= 10
cleo_return 2 num1 num2