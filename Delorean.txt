{$CLEO .s}
{$INCLUDE Common/CleoConstants.txt}
{$INCLUDE Time/Include/Variables.txt}
0000:
const
    KEY_HOVERCONVERSION = 67 // C
    KEY_DEBUG = 190 // "."
end
thread 'TIME'
gosub @Init

while true
    wait 10
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end
    // Handle local and global variables
    gosub @MemoryHandling

    // Debug
    cleo_call @DebugVariation 2 vehicle vehicle_flags ret vehicle_flags

    // Reset stuff
    cleo_call @ResetVariation 2 vehicle vehicle_flags
    cleo_call @ResetCamera 2 vehicle vehicle_flags

    // Hover Conversion
    cleo_call @HoverConversion 2 vehicle vehicle_flags ret vehicle_flags
    cleo_call @Fly 2 vehicle vehicle_flags

    cleo_call @CheckTimeMachine 1 vehicle ret result
    if
        result == false
    then
        0B11: vehicle_flags = vehicle_flags OR TC_FRIED_BIT
        continue
    end

    // Time Machine Stuff
    cleo_call @Fuel 2 vehicle vehicle_flags ret vehicle_flags
    cleo_call @Hook 2 vehicle vehicle_flags ret vehicle_flags

    // Animations
    cleo_call @FluxCapacitorAnimation 3 vehicle vehicle_flags timera ret timera
    cleo_call @FlashTimeCircuits 2 vehicle vehicle_flags

    // Check for any Time Travelling
    cleo_call @CheckPastTravel 1 vehicle ret result
    if
        result == true
    then
        gosub @Reentry
        continue
    end

    // Struck by lightning?
    // TODO: Allow the stock delorean to get struck by lightning
    cleo_call @CheckLightning 2 vehicle vehicle_flags ret result

    // Lightning check
    if
        result == true
    then
        0B11: vehicle_flags = vehicle_flags OR FC_FRIED_BIT
        0B11: vehicle_flags = vehicle_flags OR TC_FRIED_BIT
        0B11: vehicle_flags = vehicle_flags OR LIGHTNING_BIT
        gosub @TimeTravel
        continue
    end

    // Clocktower Run
    /*cleo_call @CheckWire 2 vehicle vehicle_flags ret result
    if
        result == true
    then
        gosub @SetLastDeparted
        cleo_call @Entry 2 vehicle vehicle_flags ret vehicle_flags x y z rx ry rz velocity
        cleo_call @SetTime 4 vehicle vehicle_flags dest_date dest_time
        gosub @Reentry
    end*/

    // Time Travel Effects
    cleo_call @CheckSpeed 2 vehicle vehicle_flags ret result
    if
        result == true
    then
        gosub @TimeTravel
    end
end

:TimeTravel
gosub @SetLastDeparted
cleo_call @Entry 2 vehicle vehicle_flags ret vehicle_flags x y z rx ry rz vehicle_health velocity
cleo_call @SetTime 4 vehicle vehicle_flags dest_date dest_time
gosub @Reentry
stream_custom_script "Delorean/Past.s" vehicle vehicle_flags dest_date dest_time past_date past_time 0 x y z rx ry rz velocity vehicle_health vehicle_model
return

// Include all the helper threads here
{$INCLUDE Common/DrawTexture.txt}
{$INCLUDE Common/FadeComponent.txt}
{$INCLUDE Time/Include/Camera.txt}

{$INCLUDE Time/Include/Init.txt}
{$INCLUDE Time/Include/Entry.txt}
{$INCLUDE Time/Include/Lightning.txt}
{$INCLUDE Time/Include/Memory.txt}
{$INCLUDE Time/Include/Model.txt}
{$INCLUDE Time/Include/Reentry.txt}
{$INCLUDE Time/Include/SetTime.txt}
{$INCLUDE Time/Include/Speed.txt}
{$INCLUDE Time/Include/TimeCircuits.txt}

{$INCLUDE Delorean/Include/Coils.txt}
{$INCLUDE Delorean/Include/Components.txt}
{$INCLUDE Delorean/Include/Debug.txt}
{$INCLUDE Delorean/Include/Effects.txt}
{$INCLUDE Delorean/Include/Explosion.txt}
{$INCLUDE Delorean/Include/Firetrail.txt}
{$INCLUDE Delorean/Include/FluxCapacitor.txt}
{$INCLUDE Delorean/Include/Fuel.txt}
{$INCLUDE Delorean/Include/Hook.txt}
{$INCLUDE Delorean/Include/Hover.txt}
{$INCLUDE Delorean/Include/Plasma.txt}
{$INCLUDE Delorean/Include/Plate.txt}
{$INCLUDE Delorean/Include/Setup.txt}
{$INCLUDE Delorean/Include/Sounds.txt}
{$INCLUDE Delorean/Include/TimeMachine.txt}
{$INCLUDE Delorean/Include/Wire.txt}
{$INCLUDE Delorean/Include/Wormhole.txt}

