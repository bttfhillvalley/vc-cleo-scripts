{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
const
    TEXTURE_OFFSET = 27
end
0000:
thread 'FUSMENU'
jump @WaitMenu

{$INCLUDE Menu/Common.txt}

:WaitMenu
wait 0
0AB4: vehicle = var TIME_MACHINE
0AB4: vehicle_flags = var TM_FLAGS
0AB4: garage_option = var IN_GARAGE
if and
    not Car.Wrecked(vehicle)
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    if
        garage_option == FUSION_GARAGE
    then
        jump @StartMenu
    end
end
jump @WaitMenu

:StartMenu
3F84: play_sound "garage/page1.wav" loop 0
select = 1
last_select = 0
num_options = 2
0AB3: var IN_GARAGE = FUSION_GARAGE
02D4: car vehicle turn_off_engine
041D: set_camera_near_clip 0.1
Player.CanMove($PLAYER_CHAR, false)
timera = 0

:OpenMenu
wait 0
0AB4: garage_option = var IN_GARAGE
if
    garage_option == FUSION_GARAGE
jf @WaitMenu
if or
    Car.Wrecked(vehicle)
    not Actor.InCar($PLAYER_ACTOR, vehicle)
then
    Camera.Restore()
    041D: set_camera_near_clip 0.9
    Player.CanMove($PLAYER_CHAR, true)
    jump @WaitMenu
end
if and
    select == num_options
    is_key_pressed KEY_ENTER
then
    3F84: play_sound "garage/page2.wav" loop 0
    0AB3: var IN_GARAGE = NO_GARAGE
    Camera.Restore()
    gosub @UpdateModel
    wait 100
    041D: set_camera_near_clip 0.9
    3F03: car vehicle turn_on_engine
    Player.CanMove($PLAYER_CHAR, true)
    3F2C: detach_car vehicle
    3F0C: set_car vehicle collision 1
    jump @WaitMenu
end
if
    timera > 100
then
    if
        is_key_pressed KEY_LEFT
    then
        gosub @UpdateModel
        0AB3: var IN_GARAGE = DOC_GARAGE
        jump @WaitMenu
    else if
        is_key_pressed KEY_RIGHT
    then
        gosub @UpdateModel
        0AB3: var IN_GARAGE = HOVER_GARAGE
        jump @WaitMenu
    end
    end
end


// Handle Selection
gosub @HandleKeys
gosub @FixSelection

// Show Menu
0B10: enabled = vehicle_flags AND REAR_BIT
if
    enabled == REAR_FUSION_BIT
then
    038F: load_texture 'TXGRG2' as 27 // Mr Fusion garage
    enabled = 1
else
    038F: load_texture 'TXGRG1' as 27 // Mr Fusion garage
end

cleo_call @DrawTexture params_count 9 TEXTURE_OFFSET 160.0 140.0 300.0 225.0 255 255 255 255

gosub @CheckFusion
gosub @CheckExit

// Limit options
last_select = select
jump @OpenMenu

:CheckFusion
0B10: enabled = vehicle_flags AND REAR_BIT
if
    select == 1
then
    cleo_call @DrawTexture params_count 9 CURSOR 140.0 165.0 125.0 11.0 255 255 255 255
    if and
        last_key == 0
        is_key_pressed KEY_ENTER
    then
        0B10: vehicle_flags = vehicle_flags AND REAR_BITMASK
        0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
        if
            not enabled == REAR_FUSION_BIT
        then
            0B11: vehicle_flags = vehicle_flags AND REAR_FUSION_BIT
        else
            0B11: vehicle_flags = vehicle_flags and REAR_REACTOR_BIT
        end
        gosub @ToggleOption
    end
    if
        not select == last_select
    then
         cleo_call @SetCamera 7 vehicle offset -1.0 -3.5 1.5 point_at 0.5 0.0 0.0
    end
end
return

:CheckExit
if
    select == 2
then
    cleo_call @DrawTexture params_count 9 CURSOR 115.0 240.0 70.0 11.0 255 255 255 255
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset -3.5 -5.0 1.0 point_at -1.0 0.0 0.0
    end
end
return

{$INCLUDE Common/DrawTexture.txt}

