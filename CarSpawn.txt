{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
const
    KEY_SPAWN_DELOREAN = 190 // "."
    KEY_SPAWN_JVT = 188 // ","
    KEY_SPAWN_TRUCK = 191 // "?"
    KEY_SPAWN_ROGERS = 222 // "'"
end
0000:
thread 'CARSPWN'
int model
float radius
int vehicle
float x
float y
float z
float angle
int curr_date
int curr_time
int truck_marker
int rogers_marker

while true
    wait 0
    if or
        Actor.Driving($PLAYER_ACTOR)
        not Player.Controllable($PLAYER_CHAR)
    then
        cleo_call @DisableMarker 2 TRUCK truck_marker ret truck_marker
        cleo_call @DisableMarker 2 ROGERS rogers_marker ret rogers_marker
        continue
    end
    if
        is_key_pressed KEY_SPAWN_DELOREAN // "."
    then
//        stream_custom_script "Delorean.s" DELOREAN_MODEL 0x10005908 CDATE CTIME 20241112 1000

        stream_custom_script "Delorean.s" DELOREAN_MODEL 0x10001400 CDATE CTIME 20241112 1000
        wait 1000
    end

    if
        is_key_pressed KEY_SPAWN_JVT // ","
    then
        stream_custom_script "TimeTrain.s" JVT_MODEL 0x10035000 CDATE CTIME 19540302 0553
        wait 1000
    end
    if
        is_key_pressed KEY_SPAWN_TRUCK // "?"
    then
        0AB4: vehicle = var TRUCK
        if
            Car.Wrecked(vehicle)
        then
            cleo_call @LoadModel 2 TRUCK_MODEL 4.0 ret vehicle
            stream_custom_script "Truck.s" vehicle
            0AB3: var TRUCK = vehicle
        else if
            truck_marker == 0
        then
            0186: truck_marker = create_marker_above_car vehicle
            stream_custom_script "Pickups/FlashHud.s"
        else
            stream_custom_script "Pickups/FlashHud.s"
        end
        end
    end

    0AB4: vehicle = var ROGERS
    if
        is_key_pressed KEY_SPAWN_ROGERS // "?"
    then
        if
            Car.Wrecked(vehicle)
        then
            cleo_call @LoadModel 2 ROGERS_MODEL 12.0 ret vehicle
            stream_custom_script "Rogers.s" vehicle
            0AB3: var ROGERS = vehicle
        else if
            rogers_marker == 0
        then
            0186: rogers_marker = create_marker_above_car vehicle
            stream_custom_script "Pickups/FlashHud.s"
        else
            stream_custom_script "Pickups/FlashHud.s"
        end
        end
    end
end

:LoadModel
Model.Load(model)
038B: load_requested_models
0169: set_fade_color 0 0 0
fade 0 1000
wait 1000
while not  Model.Available(model)
    wait 0
end
00BF: x = current_time_hours, y = current_time_minutes
y += 30
00C0: set_current_time x y
04C4: create_coordinate x y z from_actor $PLAYER_ACTOR offset 0.0 0.0 1.0
angle = 2
04D3: get_nearest_car_path_coords_from x y z type angle store_to x y z
while 038A:   car_in_cube x y z radius radius 3.0
    angle += 1
    04D3: get_nearest_car_path_coords_from x y z type angle store_to x y z
end
03D3: point x y z get_nearby_vector x y z angle
vehicle = Car.Create(model, x, y, z)
0175: set_car vehicle z_angle_to angle
fade 1 1000
wait 1000
Model.Destroy(model)
cleo_return 1 vehicle

:DisableMarker
if
    not 1@ == 0
then
    0AB4: 2@ = var 0@
    if
        Car.Wrecked(2@)
    then
        0164: disable_marker 1@
        1@ = 0
    else if
        Actor.InCar($PLAYER_ACTOR, 2@)
    then
        0164: disable_marker 1@
        1@ = 0
    end
    end
end
cleo_return 1 1@
