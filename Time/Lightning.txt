{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int fried_var
int hoodbox_var
int tcon_var
int mode_var
float x
float y
float z
int times
0B10: fried_var = vehicle_flags AND TC_FRIED_BIT // Time Circuits Fried
0B10: hoodbox_var = vehicle_flags AND HOOD_BIT // Hoodbox
0B10: tcon_var = vehicle_flags AND TC_BIT       // Time Circuits status
0AB4: mode_var = var MODE
if
    not hoodbox_var == 0
then
    fried_var = 0
end
if or
    not fried_var == 0 // Time Circuits are fried
    tcon_var == 0      // Time Circuits are off
then
    // Lightning sound
    3F84: play_sound "lightning.wav" loop 0
else if
    mode_var == 0 // Cutscene mode
then
    3F84: play_sound "lightning_cutscene.wav" loop 0
else
    3F84: play_sound "lightning_instant.wav" loop 0
end
end
0169: set_fade_color 255 255 255

// Set camera if we have functional time circuits for cutscene mode
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    mode_var == 0     // Cutscene mode
    fried_var == 0    // Time Circuits are not fried
    not tcon_var == 0 // Time Circuits are on
then
    0AB3: var CAMERA_CONTROL = 0
    0407: create_coordinate x y z from_car vehicle offset -15.0 10.0 7.5
    Camera.SetPosition(x, y, z, 0.0, 0.0, 0.0)
    Camera.OnVehicle(vehicle, 15, 1)
    0AB3: var CAMX = x
    0AB3: var CAMY = y
    0AB3: var CAMZ = z
end

:LightningFlash
for times = 0 to 3 step 1
    fade 0 100
    wait 100
    fade 1 100
    if
        times < 3
    then
        wait 500
    end
end
0AB3: var LIGHTNING = 2
wait 5000
// Reset Lightning just in case
0AB3: var LIGHTNING = 0
terminate_this_custom_script