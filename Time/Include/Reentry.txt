{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE Variables.txt}

:ReentryLoadModel
cleo_call @LoadModel 1 vehicle_model ret vehicle
cleo_call @Setup 6 vehicle vehicle_flags dest_date dest_time past_date past_time

// Turn on coils on a fresh model
3F12: set_car vehicle component "fluxcoilson" alpha 255
if
    result == INST_REENTRY
then
    cleo_call @ReentryExplosion 4 vehicle_flags x y z
    jump @SetVehicleData
end
repeat
    wait 0
    0AB4: result = var CFADE
until result == 0
jump @ReentryStart

:Reentry
if
    not Actor.InCar($PLAYER_ACTOR, vehicle)
then
    return
end

:ReentryStart
cleo_call @FadeIn 1 vehicle
cleo_call @ReentrySonicBoom 5 vehicle_flags x y z past_date
0B11: vehicle_flags = vehicle_flags OR COOLDOWN_BIT
timerb = 0

// Have the time vehicle come back
if
    Actor.InCar($PLAYER_ACTOR, vehicle)
else_jump @SetVehicleData
0AB3: var TRAVEL = 0
0581: toggle_radar 1
0AB4: result = MODE
if
  result == 0 // Cutscene mode?
else_jump @ReentryEnd

:SetVehicleData
0AB3: var CUTSCENE = 0
gosub @RestoreVehicleInfo
cleo_call @SetWheelStatus 2 vehicle vehicle_flags

:ReentryEnd
043C: set_game_sounds_disable_on_fade 1
gosub @SetReentryBehavior
gosub @ResetEffects
gosub @LightningSpin
gosub @ReentrySetup
return

:RestoreVehicleInfo
Car.PutAt(vehicle, x, y, z)
3F32: set_car vehicle rotation_matrix rx ry rz
Car.Health(vehicle) = vehicle_health
04BA: set_car vehicle speed_instantly 0.0
wait 10
3F35: set_car vehicle velocity_vector velocity
gosub @SetReentryBehavior
return

:SetReentryBehavior
if
    not Actor.InCar($PLAYER_ACTOR, vehicle)
then
    0AB4: result = var TIME_MACHINE
    if
        Car.Wrecked(result) // Time Machine
    then
        0AB3: var TIME_MACHINE = vehicle
    end

    0B10: result = vehicle_flags AND FLYING_BIT   // Is car flying?
    if
        result == 0
    then
        0208: result = random_float -30.0 30.0
        3F5E: set_car vehicle steering_angle result
    end
    //3F9A: set_car vehicle status STATUS_ABANDONED
    3F2D: set_car vehicle engine_sound 0
    3F9C: set_car vehicle lights 0
    wait 0
    3F5D: set_car vehicle brake_pedal 0.5
end
return
