{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE Variables.txt}

:CheckSpeed
02E3: vehicle_speed = car vehicle speed
gosub @ResetSounds
0B10: var1 = vehicle_flags AND TC_FRIED_BIT
0B10: var2 = vehicle_flags AND TC_BIT
0B10: var3 = vehicle_flags AND COOLDOWN_BIT
if or
    not var1 == 0 // Time Circuits are fried
    var2 == 0 // Time Circuits are Off
then
    cleo_call @CoilOff 2 vehicle vehicle_flags
    cleo_return 1 false
end

cleo_call @CoilOn 2 vehicle vehicle_flags

// Check Cooldown
if
    not var3 == 0 // Cooldown
then
    cleo_return 1 false
end

if
    vehicle_speed < 43.5 // == 79.8 mph
then
    cleo_return 1 false
end

// Bunch of speed related stuff for SID, coils, plasma, etc.
if
    vehicle_speed < 40.5 // == 74.3 mph
then
    cleo_return 1 false
end

// Coils start glowing around 80 mph.  Legacy for the train.
gosub @Coil
if
    vehicle_speed < 44.5 // == 81.7 mph
then
    cleo_return 1 false
end
gosub @SparkSounds

// Plasma and wormhole starts around 82 mph
gosub @Plasma

// Wormhole and sparks only pop up if fueled or if its the train
0B10: var1 = vehicle_flags AND FUEL_BIT // Are we fueled
if
    not var1 == 0 // Fueled
then
    if
        timerb > 50 // integer values
    then
        timerb = 0
        stream_custom_script "Time/Sparks.s" vehicle
    end
    gosub @Wormhole
else
    cleo_return 1 false
end

// Setup camera if we're in cutscene mode so we track the car before it time travels
if
    vehicle_speed < 47.6 // == 87.3 mph
then
    cleo_return 1 false
end
0AB4: var1 = var MODE
if and
    var1 == 0 // Cutscene Mode
    Actor.InCar($PLAYER_ACTOR, vehicle)
then
    cleo_call @GetRandomCutsceneCamera 1 vehicle
    cleo_call @SetCameraOnVehicle 1 vehicle
end

// 88 MPH!!!!!!!
3F47: var1 = get_car vehicle component "fluxcoilson" alpha
if or
    var1 < 255
    vehicle_speed < 48.1
then
    cleo_return 1 false
end
cleo_return 1 true

:CalculateSpeedOffset
3F33: get_car vehicle velocity_direction var1 var2 var3
0087: var4 = vehicle_speed
var4 /= 30.0
006B: var1 *= var4
006B: var2 *= var4
006B: var3 *= var4
return

:AddSpeedOffset
005B: x += var1
005B: y += var2
005B: z += var3
return