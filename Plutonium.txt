{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
0000:
int pickup = Pickup.Create(#SCREWDRIVER, 2, -1300.5, 173.85, 11.45)
int lybian
int driver
int gunner
int lybian_health
int travel_var
int mode_var
int marker
float x
float y
float z
0AB3: var PLUTONIUM = 12

:GetPlutoniumStart
wait 10
if
   Player.Defined($PLAYER_CHAR)
else_jump @GetPlutoniumStart
if
    Pickup.Picked_up(pickup)
jf @GetPlutoniumStart
0AB4: marker = var PLUT_MARK
if
    not marker == 0
then
    0164: disable_marker marker
    marker = 0
end
018C: play_sound 1 at -1300.5 173.85 11.45
stream_custom_script "Time/PlutoniumText.s"
0AB3: var PLUTONIUM = 12
wait 5000
terminate_all_custom_scripts_with_this_name 'PLUT'
0506: vehicle_model #RUMPO set_next_variation -1 -1
Model.Load(#RUMPO)
Model.Load(#CLA)
Model.Load(#CLB)
Model.Load(#RUGER)
Model.Load(#MP5LNG)

:GetPlutoniumLoadModels
wait 0
if and
   Model.Available(#RUMPO)
   Model.Available(#CLA)
   Model.Available(#CLB)
   Model.Available(#RUGER)
   Model.Available(#MP5LNG)
else_jump @GetPlutoniumLoadModels
lybian = Car.Create(#RUMPO, -1066.072, -55.111, 10.262)
0229: set_car lybian color_to 7 1

:GetPlutoniumCreateLybians
wait 10
if
00A3:   actor $PLAYER_ACTOR 0 -1235.0 -5000.0 -1240.0 5000.0
else_jump @GetPlutoniumCreateLybians
0129: driver = create_actor 10 #CLA in_car lybian driverseat
01C8: gunner = create_actor 10 #CLB in_car lybian passenger_seat 0
Car.Angle(lybian) = 355.752
01ED: reset_actor driver flags
011A: set_actor driver flags 1
0243: set_actor driver ped_stats_to 16
03FE: set_actor driver money 200
01B2: give_actor driver weapon 25 ammo 30000 // Load the weapon model before using this
020A: set_car lybian door_status_to 2
04BA: set_car lybian speed_instantly 10.0
00AE: set_vehicle lybian traffic_behavior_to 2
Car.SetMaxSpeed(lybian, 50.0)
Car.SetToPsychoDriver(lybian)
00AF: set_car lybian driver_behaviour_to 5
0362: put_actor gunner at -1055.072 -55.111 10.262 and_remove_from_car
0489: set_actor gunner muted 0
011C: actor gunner clear_objective
0464: put_actor gunner into_turret_on_car lybian at_car_offset 0.0 0.0 1.0 position 0 angle 360.0 with_weapon 27
01B2: give_actor gunner weapon 27 ammo 30000 // Load the weapon model before using this
011A: set_actor gunner flags 1
02E2: set_actor gunner weapon_accuracy_to 50
01CA: actor gunner kill_player $PLAYER_CHAR
0319: set_actor gunner wander_state_to 1

:GetPlutoniumLybianChase
wait 0
if
   not Car.Wrecked(lybian)
else_jump @GetPlutoniumLybianRemoveA
if
00E0:   player $PLAYER_CHAR driving
else_jump @GetPlutoniumLybianKillPlayer
0AB4: travel_var = var TRAVEL
0AB4: mode_var = var MODE
if
  travel_var == 1 // integer values
else_jump @GetPlutoniumLybianChase
if
  mode_var == 0 // integer values
else_jump @GetPlutoniumLybianDelete
wait 1000
lybian_health = Car.Health(lybian)
jump @GetPlutoniumLybianTimeTravel

:GetPlutoniumLybianDelete
Car.Destroy(lybian)
Actor.DestroyInstantly(gunner)
jump @GetPlutoniumLybianRemoveA

:GetPlutoniumLybianKillPlayer
wait 10
if
   not Car.Wrecked(lybian)
else_jump @GetPlutoniumLybianRemoveA
if
00E0:   player $PLAYER_CHAR driving
else_jump @GetPlutoniumLybianKillPlayer
01ED: reset_actor driver flags
011A: set_actor driver flags 1
0243: set_actor driver ped_stats_to 16
00AF: set_car lybian driver_behaviour_to 5
jump @GetPlutoniumLybianChase

:GetPlutoniumLybianTimeTravel
wait 10
if
   not Car.Wrecked(lybian)
else_jump @GetPlutoniumLybianRemoveA
Car.StorePos(lybian, x, y, z)
Camera.PointAt(x, y, z, 2)
if
   not fading
else_jump @GetPlutoniumLybianRemove
if
8185:   not car lybian health >= lybian_health
else_jump @GetPlutoniumLybianTimeTravel
020B: explode_car lybian
jump @GetPlutoniumLybianRemoveA

:GetPlutoniumLybianRemove
wait 1000
Car.Destroy(lybian)
Actor.DestroyInstantly(gunner)

:GetPlutoniumLybianRemoveA
Actor.RemoveReferences(gunner)
Car.RemoveReferences(lybian)
jump @GetPlutoniumStart