{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
0000:
thread 'TIMEMOD'
int year = 1985
int month = 10
int day = 25
int hour
int minute
int game_hour
int game_minute
int leapyear
int mod1
int mod2
int mod3
int set_time_var
00BF: hour = current_time_hours, minute = current_time_minutes
gosub @ConvertDate
gosub @ConvertTime

while true
    wait 0
    0AB4: set_time_var = var SET_TIME
    if
        set_time_var == 1
    then
        0AB4: day = var CDATE
        year = day
        year /= 10000
        year *= 10000
        day -= year
        year /= 10000

        month = day
        month /= 100
        month *= 100
        day -= month
        month /= 100
        00BF: hour = current_time_hours, minute = current_time_minutes
        continue
    end
    00BF: game_hour = current_time_hours, minute = current_time_minutes
    gosub @ConvertTime
    if
        not hour == game_hour
    then
        if
            game_hour > hour
        then
            hour = game_hour
            continue
        end
        hour = game_hour
        day += 1

        // Check Day
        if
            month == 2
        then
            // Check for Leap year
            0B14: mod1 = year MOD 4
            0B14: mod2 = year MOD 100
            0B14: mod3 = year MOD 400
            leapyear = 0
            if and
                mod1 == 0
                not mod2 == 0
            then
                leapyear = 1
            end
            if
                mod3 == 0
            then
                leapyear = 1
            end

            // Now check for day
            if and
                leapyear == 1
                day > 29
            then
                day = 1
                month += 1
            else if and
                leapyear == 0
                day > 28
            then
                day = 1
                month += 1
            end
            end
        else if or
            month == 4
            month == 6
            month == 9
            month == 11
        then
            if
                day > 30
            then
                day = 1
                month += 1
            end
        else
            if
                day > 31
            then
                day = 1
                month += 1
            end
        end
        end

        // Check month
        if
            month > 12
        then
            month = 1
            year += 1
        end
        gosub @ConvertDate
    end
end

:ConvertDate
year *= 100
year += month
year *= 100
year += day
0AB3: var CDATE = year
year /= 10000
return

:ConvertTime
hour *= 100
hour += minute
0AB3: var CTIME = hour
hour /= 100
return
