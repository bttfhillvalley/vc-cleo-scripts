{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
const
    KEY_RADIOCONTROL = 82 // R
    DUMMY_CAR_MODEL = 238


    RC_MODEL                = 6408
    LAST_RC_MODEL           = 6419

    RC_MODEL_LOCK           = 6409
    RC_MODEL_LOCK_LEFT      = 6410
    RC_MODEL_LOCK_RIGHT     = 6411
    RC_MODEL_BACKWARD       = 6412
    RC_MODEL_BACKWARD_LEFT  = 6413
    RC_MODEL_BACKWARD_RIGHT = 6414
    RC_MODEL_LEFT           = 6415
    RC_MODEL_RIGHT          = 6416
    RC_MODEL_FORWARD        = 6417
    RC_MODEL_FORWARD_LEFT   = 6418
    RC_MODEL_FORWARD_RIGHT  = 6419

    LEFT           = 0x01
    RIGHT          = 0x02
    FORWARD        = 0x04
    BACKWARD       = 0x08

    FORWARD_LEFT   = 0x05
    FORWARD_RIGHT  = 0x06

    BACKWARD_LEFT  = 0x09
    BACKWARD_RIGHT = 0x0A

    LOCK           = 0x0C
    LOCK_LEFT      = 0x0D
    LOCK_RIGHT     = 0x0E
end
0000:
thread 'REMOTE'
int vehicle
int camera_mode = 15
float x
float y
float z
float vehicle_speed
int controls
int dummy_vehicle
int dummy_actor
int dummy_actor_struct
int model_id
int new_model_id
float player_angle
float player_health

:RadioControlStart
wait 0
0AB4: vehicle = var TIME_MACHINE

if
    Car.Wrecked(vehicle)
then
    0AD1: show_formatted_text_highpriority "Car wrecked! %x" time 10 vehicle
else
    0AD1: show_formatted_text_highpriority "Car not wrecked! %x" time 10 vehicle
end

if and
    not Actor.Driving($PLAYER_ACTOR)
    not Car.Wrecked(vehicle)
    is_key_pressed KEY_RADIOCONTROL // R
    //02D7:   player $PLAYER_CHAR current_weapon == 2
    timera > 500
else_jump @RadioControlStart
timera = 0

// Load models
Model.Load(DUMMY_CAR_MODEL) //DMC-12
for model_id = RC_MODEL to LAST_RC_MODEL
    Model.Load(model_id) //RC REMOTE
end
038B: load_requested_models

// Wait for models to load
while not Model.Available(DUMMY_CAR_MODEL)
    wait 0
end
for model_id = RC_MODEL to LAST_RC_MODEL
    while not Model.Available(model_id)
        wait 0
    end
end
model_id = -1

// Set camera first
04C4: create_coordinate x y z from_actor $PLAYER_ACTOR offset 0.0 0.0 1.0
Camera.SetPosition(x, y, z, 0.0, 0.0, 0.0)
Camera.OnVehicle(vehicle, camera_mode, 1)

// Create Dummy car
04C4: create_coordinate x y z from_actor $PLAYER_ACTOR offset 0.0 0.0 -1.0
dummy_vehicle = Car.Create(DUMMY_CAR_MODEL, x, y, 5000.0) //DMC-12
0369: put_player $PLAYER_CHAR in_car dummy_vehicle
02D4: car dummy_vehicle turn_off_engine

// Create Dummy Actor
player_angle = Actor.Angle($PLAYER_ACTOR)
player_health = Actor.Health($PLAYER_ACTOR)
009A: dummy_actor = create_actor_pedtype 4 model #NULL at x y z //player model
Actor.Angle(dummy_actor) = player_angle
Actor.Health(dummy_actor) = player_health
04C4: create_coordinate x y z from_actor dummy_actor offset 0.0 -1.0 0.0
0245: set_actor dummy_actor walk_style_to 33
05E6: dummy_actor_struct = actor dummy_actor struct
0AA6: call_method 0x004FFE40 struct dummy_actor_struct num_params 1 pop 1 RC_MODEL

// Set initial Angle
0407: create_coordinate x y z from_car vehicle offset 0.0 0.0 -0.75
01BE: set_actor dummy_actor to_look_at_spot x y z
04ED: load_animation 'STRIP'
0673: play_animation on actor DUMMY_ACTOR animgroup 28 anim 226 blendfactor 1.0

// Set Remote mode
3F03: car vehicle turn_on_engine
3F23: set_car vehicle remote
04D6: enable_rc_car_detonation 0
048A: enable_rc_vehicle_detonation 0

:RadioControlLoop
wait 0

if and
   // 0202:   actor dummy_actor near_car vehicle radius 120.0 120.0 sphere 0
    3F2A:   is_player_in_remote_mode_with_car vehicle
    not Car.Wrecked(vehicle)
else_jump @RadioControlExit
if and
    is_key_pressed KEY_RADIOCONTROL
    timera > 500
then
    timera = 0
    jump @RadioControlExit
end
if and
    00E1:   player 0 pressed_button 13
    timera > 500
then
    timera = 0
    if
        camera_mode == 3
    then
        camera_mode = 15 // Tracking
    else
        camera_mode = 3 // Behind car
    end
end

// Keep track of Delorean and have the dummy car over it to keep cars loaded
0407: create_coordinate x y z from_car vehicle offset 0.0 0.0 -0.75
Car.PutAt(dummy_vehicle, x, y 5000.0)
01BE: set_actor dummy_actor to_look_at_spot x y z
//0372: set_actor dummy_actor anim 25 wait_state_time 99999999 ms
04C4: create_coordinate x y z from_actor dummy_actor offset 0.0 -0.59 0.85
Camera.SetPosition(x, y, z, 0.0, 0.0, 2.0)
Camera.OnVehicle(vehicle, camera_mode, 2)
02E3: vehicle_speed = car vehicle speed

// Store Controls
controls = 0

// Gas
if
    00E1:   player 0 pressed_button 16
then
    0B11: controls = controls OR FORWARD
end

// Brake/Reverse
if
    00E1:   player 0 pressed_button 14
then
    0B11: controls = controls OR BACKWARD
end

// Left
if
    00E1:   player 0 pressed_button 10
then
    0B11: controls = controls OR LEFT
end

// Right
if
    00E1:   player 0 pressed_button 11
then
    0B11: controls = controls OR RIGHT
end

// Load correct model

// Idle
new_model_id = RC_MODEL

if
    controls == LEFT
then
    new_model_id = RC_MODEL_LEFT
end

if
    controls == RIGHT
then
    new_model_id = RC_MODEL_RIGHT
end

if
    controls == FORWARD
then
    new_model_id = RC_MODEL_FORWARD
end

if
    controls == FORWARD_LEFT
then
    new_model_id = RC_MODEL_FORWARD_LEFT
end

if
    controls == FORWARD_RIGHT
then
    new_model_id = RC_MODEL_FORWARD_RIGHT
end

if
    controls == BACKWARD
then
    new_model_id = RC_MODEL_BACKWARD
end

if
    controls == BACKWARD_LEFT
then
    new_model_id = RC_MODEL_BACKWARD_LEFT
end

if
    controls == BACKWARD_RIGHT
then
    new_model_id = RC_MODEL_BACKWARD_RIGHT
end

if and
    controls == LOCK
    vehicle_speed < 1.0
then
    new_model_id = RC_MODEL_LOCK
end

if
    controls == LOCK_LEFT
then
    if
        vehicle_speed < 1.0
    then
        new_model_id = RC_MODEL_LOCK_LEFT
    else
        new_model_id = RC_MODEL_LEFT
    end
end

if
    controls == LOCK_RIGHT
then
    if
        vehicle_speed < 1.0
    then
        new_model_id = RC_MODEL_LOCK_RIGHT
    else
        new_model_id = RC_MODEL_RIGHT
    end
end

// Load model
if
    not model_id == new_model_id
then
    model_id = new_model_id
    0AA6: call_method 0x004FFE40 struct dummy_actor_struct num_params 1 pop 1 model_id
end
jump @RadioControlLoop

:RadioControlExit
3F24: remove_car vehicle remote
04C4: create_coordinate x y z from_actor dummy_actor offset 0.0 0.0 -1.0
player_angle = Actor.Angle(dummy_actor)
player_health = Actor.Health(dummy_actor)
Actor.DestroyInstantly(dummy_actor)
012A: put_player $PLAYER_CHAR at x y z and_remove_from_car
Actor.Angle($PLAYER_ACTOR) = player_angle
Actor.Health($PLAYER_ACTOR) = player_health
Car.Destroy(dummy_vehicle)

// Unload Models
Model.Destroy(DUMMY_CAR_MODEL) //DMC-12
for model_id = RC_MODEL to LAST_RC_MODEL
    Model.Destroy(model_id) //RC REMOTE
end

// Restore Camera
if
    camera_mode == 3
then
    Camera.Restore_WithJumpCut
else
    Camera.Restore
end
Camera.SetBehindPlayer
Player.CanMove($PLAYER_CHAR) = False
wait 500
Player.CanMove($PLAYER_CHAR) = True
jump @RadioControlStart