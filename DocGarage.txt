{$CLEO .cs}
{$INCLUDE Common/CleoConstants.txt}
const
    TMG = 17
    COMPONENTS = 19
    HOOK = 20
    HOOK_HOLDER = 21
    ALARM_CLOCK = 22
    BOILER_GAUGE = 23
    HOOD_BOX = 24
    HITCH = 25
    EXIT = 26
    REPAIR = 71

    KEY_SPAWN_MENU = 71 // G
end
0000:
thread 'DOCMENU'
jump @WaitMenu

{$INCLUDE Menu/Common.txt}

:WaitMenu
wait 0
0AB4: vehicle = var TIME_MACHINE
0AB4: vehicle_flags = var TM_FLAGS
0AB4: garage_option = var IN_GARAGE
0AB4: current_date = var CDATE
0AB4: option = var ACTION_LOCK
0AB4: select = var TRUCK
0B10: enabled = vehicle_flags AND FLYING_BIT
if and
    not Car.Wrecked(vehicle)
    not Car.Wrecked(select)
    Actor.InCar($PLAYER_ACTOR, vehicle)
    option == 0
    enabled == 0
then
    0407: create_coordinate x y w from_car select offset 0.0 -2.1 0.9
    if and
        01AF:   car vehicle 0 x y w radius 0.3 0.3 0.3
        garage_option == NO_GARAGE
        00E1: key_pressed 0 19
    then
        jump @StartMenu
    end
    if
       garage_option == DOC_GARAGE
    then
        jump @StartMenu
    end
end
jump @WaitMenu

:StartMenu
3F84: play_sound "garage/page1.wav" loop 0
select = 1
last_select = 0
num_options = 99
0AB3: var IN_GARAGE = DOC_GARAGE
02D4: car vehicle turn_off_engine
041D: set_camera_near_clip 0.1
Player.CanMove($PLAYER_CHAR, false)
timera = 0

:OpenMenu
wait 0
0AB4: garage_option = var IN_GARAGE
if
    garage_option == DOC_GARAGE
jf @WaitMenu
if or
    Car.Wrecked(vehicle)
    not Actor.InCar($PLAYER_ACTOR, vehicle)
then
    Camera.Restore()
    041D: set_camera_near_clip 0.9
    Player.CanMove($PLAYER_CHAR, true)
    jump @WaitMenu
end
if
    select == num_options
then
    if or
        is_key_pressed KEY_ENTER
        3F75: is_analgoue_pressed 0 5
    then
        3F84: play_sound "garage/page2.wav" loop 0
        0AB3: var IN_GARAGE = NO_GARAGE
        Camera.Restore()
        gosub @UpdateModel
        wait 100
        041D: set_camera_near_clip 0.9
        3F03: car vehicle turn_on_engine
        Player.CanMove($PLAYER_CHAR, true)
        jump @WaitMenu
    end
end
if
    timera > 100
then
    if or
        is_key_pressed KEY_LEFT
        3F75: is_analogue_pressed 0 3
    then
        gosub @UpdateModel
        if
            current_date >= 20100101
        then
            0AB3: var IN_GARAGE = HOVER_GARAGE
        else
            0AB3: var IN_GARAGE = DMC_GARAGE
        end
        jump @WaitMenu
    else if or
        is_key_pressed KEY_RIGHT
        3F75: is_analogue_pressed 0 4
    then
        gosub @UpdateModel
        0AB3: var IN_GARAGE = DMC_GARAGE
        jump @WaitMenu
    end
    end
end

// Handle Selection
gosub @HandleKeys
gosub @FixSelection

// Show Menu
if
    current_date < 19650101
then
    cleo_call @DrawTexture params_count 9 TMG 160.0 160.0 300.0 300.0 255 255 255 255
    038F: load_texture 'TMG1' as 17 // Garage Background
else
    cleo_call @DrawTexture params_count 9 TMG 160.0 160.0 300.0 300.0 255 255 255 255
    038F: load_texture 'TMG3' as 17 // Garage Background
end

// Show Options
gosub @InitOptions
0B10: enabled = vehicle_flags AND REAR_BIT
if
    enabled == REAR_NONE_BIT
then
    gosub @DrawComponents
else
    0B10: enabled = vehicle_flags AND TC_FRIED_BIT
    if
        enabled == 0
    then
        gosub @DrawComponents
    else
        gosub @DrawRepair
    end
end
0B10: enabled = vehicle_flags AND REAR_BIT
if
    enabled == REAR_REACTOR_BIT
then
    gosub @DrawHook
    gosub @DrawHookHolder
end
0B10: enabled = vehicle_flags AND REAR_BIT
if
    not enabled == REAR_NONE_BIT
then
    gosub @DrawAlarmClock
    gosub @DrawBoilerGauge
    gosub @DrawHoodBox
    gosub @DrawHitch
end
gosub @DrawExit

// Limit options
last_select = select
num_options = option
jump @OpenMenu

:InitOptions
option = 0
y = 50.0
return

:NewOption
option += 1
y += 30.0
return

:DrawOption
if
    enabled == 0
then
    enabled = 128
else
    enabled = 255
end
w *= 0.65
x = w
x /= 2.0
x += 20.0

cleo_call @DrawTexture params_count 9 texture x y w h enabled enabled enabled 255
if
    select == option
then
    y += 15.0
    cleo_call @DrawTexture params_count 9 CURSOR 120.0 y 200.0 11.0 255 255 255 255
end
cleo_return 0

:DrawComponents
gosub @NewOption
0B10: enabled = vehicle_flags AND REAR_BIT
if
    enabled > REAR_NONE_BIT
then
    enabled = 1
else
    enabled = 0
end
cleo_call @DrawOption params_count 7 COMPONENTS 384.5 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B10: vehicle_flags = vehicle_flags AND TC_FRIED_BITMASK
            0B10: vehicle_flags = vehicle_flags AND REAR_BITMASK
            0B10: vehicle_flags = vehicle_flags AND PLATE_BITMASK
            if
                enabled == 0
            then
                0B12: vehicle_flags = vehicle_flags XOR REAR_REACTOR_BIT
                0B12: vehicle_flags = vehicle_flags XOR PLATE_OUTATIME_BIT
            else
                0B10: vehicle_flags = vehicle_flags AND FUEL_BITMASK
                0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
                0B10: vehicle_flags = vehicle_flags AND CLOCK_BITMASK
                0B10: vehicle_flags = vehicle_flags AND HOOD_BITMASK
                0B10: vehicle_flags = vehicle_flags AND HITCH_BITMASK
                0B10: vehicle_flags = vehicle_flags AND BOILER_BITMASK
            end
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 4.5 -6.0 1.0 point_at -2.5 0.5 0.0
    end
end
return

:DrawRepair
gosub @NewOption
if
    current_date > 19850101
then
    enabled = 1
else
    enabled = 0
end
cleo_call @DrawOption params_count 7 71 68.0 15.0 y option select enabled
if
    select == option
then
    if and
        last_key == 0
        enabled == 1
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B10: vehicle_flags = vehicle_flags AND TC_FRIED_BITMASK
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 4.5 -6.0 1.0 point_at -2.5 0.5 0.0
    end
end
return


:DrawHook
gosub @NewOption
0B10: enabled = vehicle_flags AND HOOK_BIT
if
    enabled > HOOK_HOLDER_BIT
then
    enabled = 1
else
    enabled = 0
end
cleo_call @DrawOption params_count 7 HOOK 265.5 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
            if
                enabled == 0
            then
                0B12: vehicle_flags = vehicle_flags XOR HOOK_SIDE_BIT
            end
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 0.5 -2.5 1.0 point_at 1.0 0.5 0.0
    end
end
return

:DrawHookHolder
gosub @NewOption
0B10: enabled = vehicle_flags AND HOOK_BIT
if
    enabled >= HOOK_HOLDER_BIT
then
    enabled = 1
else
    enabled = 0
end
cleo_call @DrawOption params_count 7 HOOK_HOLDER 250.0 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B10: vehicle_flags = vehicle_flags AND HOOK_BITMASK
            if
                enabled == 0
            then
                0B12: vehicle_flags = vehicle_flags XOR HOOK_HOLDER_BIT
            end
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 1.5 -3.0 1.0 point_at -2.5 0.5 0.0
    end
end
return

:DrawAlarmClock
gosub @NewOption
0B10: enabled = vehicle_flags AND CLOCK_BIT
cleo_call @DrawOption params_count 7 ALARM_CLOCK 138.75 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B12: vehicle_flags = vehicle_flags XOR CLOCK_BIT
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset -0.4 0.0 0.4 point_at 0.0 1.0 0.1
    end
end
return


:DrawBoilerGauge
gosub @NewOption
0B10: enabled = vehicle_flags AND BOILER_BIT
cleo_call @DrawOption params_count 7 BOILER_GAUGE 304.375 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B12: vehicle_flags = vehicle_flags XOR BOILER_BIT
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 0.2 -0.2 0.3 point_at 0.1 1.0 0.0
    end
end
return

:DrawHoodBox
gosub @NewOption
0B10: enabled = vehicle_flags AND HOOD_BIT
cleo_call @DrawOption params_count 7 HOOD_BOX 420.0 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B12: vehicle_flags = vehicle_flags XOR HOOD_BIT
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 0.9 4.3 0.1 point_at 0.5 1.0 0.0
    end
end
return

:DrawHitch
gosub @NewOption
0B10: enabled = vehicle_flags AND HITCH_BIT
cleo_call @DrawOption params_count 7 HITCH 304.5 15.0 y option select enabled
if
    select == option
then
    if
        last_key == 0
    then
        if or
            is_key_pressed KEY_ENTER
            3F75: is_analogue_pressed 0 5
        then
            0B12: vehicle_flags = vehicle_flags XOR HITCH_BIT
            gosub @ToggleOption
        end
    end
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 0.9 4.3 -0.2 point_at 0.5 1.0 -0.5
    end
end
return

:DrawExit
gosub @NewOption
cleo_call @DrawOption params_count 7 EXIT 123.0 15.0 y option select 1
if
    select == option
then
    if
        not select == last_select
    then
        cleo_call @SetCamera 7 vehicle offset 4.5 -6.0 1.0 point_at -2.5 0.5 0.0
    end
end
return

{$INCLUDE Common/DrawTexture.txt}