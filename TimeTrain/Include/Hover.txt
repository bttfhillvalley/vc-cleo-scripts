{$INCLUDE ../../Common/CleoConstants.txt}
{$INCLUDE ../../Time/Include/Variables.txt}

:HoverConversion
// Check To see if we're going to hover.
if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
then
    // Can we convert?
    0B10: var1 = vehicle_flags AND FC_BIT  // Flying Circuits
    0AB4: var2 = var CONVERSION
    if and
        not var1 == 0  // Flying circuits are installed
        var2 == 0  // Not animating
        //is_key_pressed KEY_HOVERCONVERSION
        00E1: key_pressed 0 19
    then
        // Can only animated if flying circuits are fried
        0AB3: var CONVERSION = 1
        0AB3: var LANDING = 1
        0B10: var1 = vehicle_flags AND FC_FRIED_BIT // Fried
        0B10: var2 = vehicle_flags AND FLYING_BIT   // Is car flying?
        if or
            var1 == 0 // Flying circuits are not fried
            not var2 == 0 // Flying?
        then
            0B12: vehicle_flags = vehicle_flags XOR FLYING_BIT  // Flip flying bit
            stream_custom_script "TimeTrain/HoverAnimate.s" vehicle vehicle_flags
        end
    end
end
cleo_return 1 vehicle_flags

:Fly
3F10: set_car vehicle component "locothrusteron" visible 0
3F10: set_car vehicle component "locothrusterth" visible 0
3F10: set_car vehicle component "bogie1locothrusteron" visible 0
3F10: set_car vehicle component "bogie1locothrusterth" visible 0
0B10: var1 = vehicle_flags AND FLYING_BIT   // Is car flying?
0B10: var2 = vehicle_flags AND FC_FRIED_BIT   // Is car flying?
0AB4: var3 = var LANDING
if or
    var1 == 0
    not var2 == 0
then
    cleo_call @FadeComponent 4 vehicle "inner_vents" 0 15
    cleo_call @FadeComponent 4 vehicle "inner_ventsglow" 0 15
    if
        var3 == 0
    then
        jump @FlyReturn
    end
end
3F05: set_car vehicle hover 1
3F02: var1 = car vehicle engine_state
0AB4: var2 = var CONVERSION

// Show wheel lights only when we're not converting
if
    not var1 == 0  // Engine On
then
    3F1C: get_car vehicle component "wheels_r_dummy" rotation var1 var2 var3
    0097: abs var2
    if
        var2 > 89.0
    then
        cleo_call @FadeComponent 4 vehicle "inner_vents" 255 15
    end
    3F10: set_car vehicle component "locothrusteron" visible 1
    3F10: set_car vehicle component "bogie1locothrusteron" visible 1
end

if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
else_jump @FlyReturn
Car.StorePos(vehicle, x, y, z)

3F02: var1 = car vehicle engine_state
if and
    z < HEIGHT_LIMIT
    not var1 == 0
else_jump @FlyReset

0AB4: var1 = var LANDING
if and
    00E1:   key_pressed 0 6
    80E1:   not key_pressed 0 HOVER_ACCEL_KEY
then
    02E3: vehicle_speed = car vehicle speed
    vehicle_speed *= -0.1
    vehicle_speed += 11.0
    vehicle_speed *= 10.0
    3F25: set_car vehicle forward_force vehicle_speed
    cleo_call @FadeComponent 4 vehicle "inner_ventsglow" 0 50
else if and
    80E1:   not key_pressed 0 6
    00E1:   key_pressed 0 HOVER_ACCEL_KEY
then
    vehicle_speed = -5.0
    vehicle_speed *= 10.0
    3F25: set_car vehicle forward_force vehicle_speed
    cleo_call @FadeComponent 4 vehicle "inner_ventsglow" 0 50
else if and
    00E1:   key_pressed 0 6
    00E1:   key_pressed 0 HOVER_ACCEL_KEY
    var1 == 0
then
    cleo_call @FadeComponent 4 vehicle "inner_ventsglow" 255 50
    02E3: vehicle_speed = car vehicle speed
    vehicle_speed *= -0.1
    vehicle_speed += 20.0
    vehicle_speed *= 10.0
    3F25: set_car vehicle forward_force vehicle_speed
    // Vent Sounds
    0AB4: var1 = var VSOUND
    0AB4: var2 = var TRAVEL
    if and
        var1 == 0 // not loaded
        var2 == 0
    then
        3F96: var1 = attach_sound "jvt/boost.wav" to_car vehicle offset 0 -8.0 2.0 loop 0 size 30.0
        0AB3: var VSOUND = var1
    end
else
    cleo_call @FadeComponent 4 vehicle "inner_ventsglow" 0 50
end
end
end
if or
    80E1:   not key_pressed 0 6
    80E1:   not key_pressed 0 HOVER_ACCEL_KEY
then
    jump @FlyReset
end
jump @FlyReturn

:FlyReset
0AB3: var VSOUND = 0


:FlyReturn
cleo_return 0

:CalculateTenderSpeedOffset
// Tender offset
2@ -= TENDER_Y
3@ -= TENDER_Z
3F1D: get_car vehicle component "tender" offset 1@ 2@ 3@ store_to 1@ 2@ 3@
02E3: 4@ = car vehicle speed
if
    not 4@ == 0.0
then
    4@ /= 30.0
    3F33: get_car vehicle velocity_direction 5@ 6@ 7@
    006B: 5@ *= 4@
    006B: 6@ *= 4@
    006B: 7@ *= 4@
    005B: 1@ += 5@
    005B: 2@ += 6@
    005B: 3@ += 7@
end
cleo_return 3 1@ 2@ 3@