{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int flying_flag
int travel_var
float vehicle_speed
float x
float y
float z
float floor
float frame
int ignore

0B10: flying_flag = vehicle_flags AND FLYING_BIT   // Is car flying?

if
    flying_flag == FLYING_LANDING_BIT
then
    frame = 150.0
    3F96: ignore = attach_sound "jvt/hover_retract.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
else
    frame = 0.0
    3F96: ignore = attach_sound "jvt/hover_extend.wav" to_car vehicle offset 0 0 0 loop 0 size 30.0
    stream_custom_script "TimeTrain/Thrust.s" vehicle
    3F28: set_car vehicle wheel_status 2 // Remove tires
end

:HoverAnimateStart
wait 10
if
    not Car.Wrecked(vehicle)
else_jump @HoverAnimateEnd
0AB4: travel_var = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    not travel_var == 0
then
    jump @HoverAnimateStart
end
gosub @CheckFrame
cleo_call @BackWing 2 vehicle frame
cleo_call @TopWing 2 vehicle frame
cleo_call @Wheel 2 vehicle frame
cleo_call @Holder 2 vehicle frame
cleo_call @Wing 2 vehicle frame
jump @HoverAnimateStart

:CheckFrame
if
    flying_flag == FLYING_LANDING_BIT
then
    if
        frame > 0.0
    then
        frame -= 1.0
    else
        3F28: set_car vehicle wheel_status 0
        jump @HoverAnimateEnd
    end

else
    if
        frame < 150.0
    then
        frame += 1.0
    else
        jump @HoverAnimateEnd
    end
end
return

:CalculateTenderSpeedOffset
// Tender offset
2@ -= TENDER_Y
3@ -= TENDER_Z
3F1D: get_car vehicle component "tender" offset 1@ 2@ 3@ store_to 1@ 2@ 3@
02E3: 4@ = car vehicle speed
if
    not 4@ == 0.0
then
    4@ /= 30.0
    3F33: get_car vehicle velocity_direction 5@ 6@ 7@
    006B: 5@ *= 4@
    006B: 6@ *= 4@
    006B: 7@ *= 4@
    005B: 1@ += 5@
    005B: 2@ += 6@
    005B: 3@ += 7@
end
cleo_return 3 1@ 2@ 3@

:BackWing
if
    1@ > 50.0
then
    1@ = 50.0
end
1@ *= -0.36
3F16: set_car vehicle component "backwing_1" angle 1@ 0.0 0.0
3F16: set_car vehicle component "backwing_2" angle 1@ 0.0 0.0
3F16: set_car vehicle component "backwing_3" angle 1@ 0.0 0.0
3F16: set_car vehicle component "backwing_4" angle 1@ 0.0 0.0
3F16: set_car vehicle component "backwing_5" angle 1@ 0.0 0.0
cleo_return 0

:TopWing
3F10: set_car vehicle component "topwing_1_l" visible 1@
3F10: set_car vehicle component "topwing_2_l" visible 1@
3F10: set_car vehicle component "topwing_3_l" visible 1@
3F10: set_car vehicle component "topwing_1_r" visible 1@
3F10: set_car vehicle component "topwing_2_r" visible 1@
3F10: set_car vehicle component "topwing_3_r" visible 1@
if
    1@ > 36.0
then
    1@ = 36.0
end
1@ *= -1.0
0087: x = 1@
x /= -1.8
0087: z = 1@
z *= 1.25
3F16: set_car vehicle component "topwing_1_l" angle 1@ -60.0 z
3F16: set_car vehicle component "topwing_2_l" angle x 0.0 0.0
3F16: set_car vehicle component "topwing_3_l" angle x 0.0 0.0
z *= -1.0
3F16: set_car vehicle component "topwing_1_r" angle 1@ 60.0 z
3F16: set_car vehicle component "topwing_2_r" angle x 0.0 0.0
3F16: set_car vehicle component "topwing_3_r" angle x 0.0 0.0
cleo_return 0

:CalculateWheelStrutAngle
0@ -= 0.38303
0@ *= -1.0
0087: 1@ = 0@

// a^2 + b^2 - c^2
006B: 0@ *= 0@
0@ += 0.101989
// 2 * a * b
1@ *= 0.659729

// math.arcos
0073: 0@ /= 1@

3F57: 0@ = arccosine 0@
0@ -= 10.201068

cleo_return 1 0@

:Wheel
if
    1@ > 45.0
then
    1@ = 45.0
end
1@ *= -2.0

3F16: set_car vehicle component "wheels_l_" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hoverbottom_l" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hovermid_r" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hovertop_l" angle 0.0 1@ 0.0
1@ *= -1.0
3F16: set_car vehicle component "wheels_r_" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hoverbottom_r" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hovermid_l" angle 0.0 1@ 0.0
3F16: set_car vehicle component "hovertop_r" angle 0.0 1@ 0.0
1@ *= 0.00129733
3F14: set_car vehicle component "hoverpiston_l" pos 0.0 0.0 1@
3F14: set_car vehicle component "hoverpiston_r" pos 0.0 0.0 1@

cleo_call @CalculateWheelStrutAngle 1 1@ ret 1@
3F16: set_car vehicle component "hoverstrut_r" angle 0.0 1@ 0.0
1@ *= -1.0
3F16: set_car vehicle component "hoverstrut_l" angle 0.0 1@ 0.0
cleo_return 0

:Holder
1@ *= -0.02
1@ += 0.9
if
    1@ > 0.0
then
    1@ = 0.0
end
if
    1@ < -0.34
then
    1@ = -0.34
end

3F14: set_car vehicle component "holder_l" pos 1@ 0.0 0.0
cleo_call @WingDoor 3 vehicle "wingdoor_l" 1@
1@ *= -1.0
3F14: set_car vehicle component "holder_r" pos 1@ 0.0 0.0
cleo_call @WingDoor 3 vehicle "wingdoor_r" 1@
cleo_return 0

:WingDoor
2@ *= -300.0
if
    2@ < -80.0
then
    2@ = -80.0
end
if
    2@ > 80.0
then
    2@ = 80.0
end
3F16: set_car vehicle component 1@ angle 0.0 2@ 0.0
cleo_return 0

:Wing
1@ -= 71.0
if
    1@ < 0.0
then
    1@ = 0.0
end

if
    1@ <= 15.0
then
    x = -0.002
    y = 5.685
    0087: z = 1@
    z *= -1.06
    z += 9.5
else if
    1@ <= 25.0
then
    x = -0.002
    0087: y = 1@
    y *= -0.0115
    y += 5.8575
    0087: z = 1@
    z *= -1.06
    z += 9.5
else if
    1@ <= 35.0
then
    x = -0.002
    0087: y = 1@
    y *= -0.036
    y += 6.47
    0087: z = 1@
    z *= -0.3665
    z -= 7.8375
else if
    1@ <= 39.0
then
    0087: y = 1@
    y *= -0.0725
    y += 7.7475
    x = y
    x *= -1.494135
    x += 15.61315
    x *= y
    x -= 40.78827
    0087: z = 1@
    z *= -0.0375
    z -= 19.3525
else
    0087: floor = 1@
    floor -= 40.0
    floor *= 2.0
    floor += 1.0
    if
        floor > 39.0
    then
        floor = 39.0
    end
    x = floor
    x *= 2.09888E-05
    x -= 0.0009456733
    x *= floor
    x -= 0.02509198
    x *= floor
    x -= 0.1379266
    y = floor
    y *= 6.4025E-06
    y += 8.592E-07
    y *= floor
    y -= 0.02928919
    y *= floor
    y += 4.920224
    if
        floor <= 19.0
    then
        z = floor
        z *= -0.0006994204
        z += 0.04098213
        z *= floor
        z += 0.002020355
        z *= floor
        z += -20.84815
    else if
        floor <= 35.0
    then
        z = floor
        z *= -0.001519528
        z += 0.1089485
        z *= floor
        z += -1.79962
        z *= floor
        z += -5.547737
    else
        z = 0.0
    end
    end
end
end
end
end
z -= 10.0
3F14: set_car vehicle component "middlewing_1_l" pos x y 0.0
3F16: set_car vehicle component "middlewing_1_l" angle 0.0 0.0 z
x *= -1.0
z *= -1.0
3F14: set_car vehicle component "middlewing_1_r" pos x y 0.0
3F16: set_car vehicle component "middlewing_1_r" angle 0.0 0.0 z

if
    1@ <= 59.0
then
    x = 0.0
    y = 0.0
else
    0087: x = 1@
    x *= -0.515
    x += 30.385
    0087: y = 1@
    y *= -0.745
    y += 43.955
end
3F16: set_car vehicle component "middlewing_2_l" angle 0.0 0.0 x
3F16: set_car vehicle component "middlewing_3_l" angle 0.0 0.0 y
x *= -1.0
y *= -1.0
3F16: set_car vehicle component "middlewing_2_r" angle 0.0 0.0 x
3F16: set_car vehicle component "middlewing_3_r" angle 0.0 0.0 y
cleo_return 0

:HoverAnimateEnd
terminate_this_custom_script
