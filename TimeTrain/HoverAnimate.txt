{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int vehicle_flags
int flying_flag
int travel_var
float vehicle_speed
float x
float y
float z
float floor
float frame
int ignore

0B10: flying_flag = vehicle_flags AND FLYING_BIT   // Is car flying?

if
    flying_flag == FLYING_LANDING_BIT
then
    frame = 150.0
    3F86: attach_sound "jvt/hover_retract.wav" to_car vehicle offset 0 0 0 loop 0 size 10.0
    3F28: set_car vehicle wheel_status 0 // Add tires
else
    frame = 0.0
    3F86: attach_sound "jvt/hover_extend.wav" to_car vehicle offset 0 0 0 loop 0 size 30.0
    3F28: set_car vehicle wheel_status 2 // Remove tires
    03ED: set_car vehicle not_damaged_when_upside_down 1
    3F69: shake_pad 0 duration 300 freq 70
end

:HoverAnimateStart
wait 10
if
    not Car.Wrecked(vehicle)
else_jump @HoverAnimateEnd
0AB4: travel_var = var TRAVEL
if and
    Actor.InCar($PLAYER_ACTOR, vehicle)
    not travel_var == 0
then
    jump @HoverAnimateStart
end
gosub @CheckFrame
cleo_call @BackWing 2 vehicle frame
cleo_call @TopWing 2 vehicle frame
cleo_call @Wheel 2 vehicle frame
cleo_call @Holder 2 vehicle frame
cleo_call @Wing 2 vehicle frame
jump @HoverAnimateStart

:CheckFrame
if
    flying_flag == FLYING_LANDING_BIT
then
    if
        frame > 0.0
    then
        frame -= 1.0
    else
        jump @HoverAnimateEnd
    end

else
    if
        frame < 150.0
    then
        frame += 1.0
        if
            frame <= 60.0
        then
            cleo_call @Chuf 2 vehicle frame
        end
    else
        jump @HoverAnimateEnd
    end
end
return

:HoverAnimateEnd
terminate_this_custom_script

{$INCLUDE Include/HoverAnimateModel.txt}
