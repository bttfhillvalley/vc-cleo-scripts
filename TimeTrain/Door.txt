{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
int door_state
int exit_car
float door_angle
float step_angle
float x
float y
float z

020A: set_car vehicle door_status_to 2
while true
    wait 10
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end

    0407: create_coordinate x y z from_car vehicle offset -2.0 -1.4 0.0
    if and
        00E1:   key_pressed 0 4
        00FF:   actor $PLAYER_ACTOR 0 x y z radius 0.75 0.75 2.0
        timera > 500
    then
        timera = 0
        door_state *= -1
        door_state += 1
        gosub @PlayDoorSound
    end

    if
        Actor.InCar($PLAYER_ACTOR, vehicle)
    then
        if
            exit_car == 0
        then
            if
                00E1: key_pressed 0 15
            then
                if
                    door_state == 0
                then
                    door_state = 1
                    gosub @PlayDoorSound
                end
                exit_car = 1
            else if
                door_state == 1
            then
                door_state = 0
                gosub @PlayDoorSound
            end
            end
        end
    else
        exit_car = 0
    end

    if
        door_state == 1
    then
        3F9F: set_car vehicle door DOOR_FRONT_LEFT status DOOR_STATUS_MISSING
        if
            door_angle < 59.5
        then
            door_angle += 0.8
        else
            gosub @StopDoorSound
        end
    else
        3F9F: set_car vehicle door DOOR_FRONT_LEFT status DOOR_STATUS_OK
        if
            door_angle > 0.5
        then
            door_angle -= 0.8
        else
            gosub @StopDoorSound
        end
    end
    step_angle = door_angle
    step_angle *= -2.7
    3F16: set_car vehicle component "door_up_hi_ok" angle 0.0 door_angle 0.0
    3F16: set_car vehicle component "door_down" angle 0.0 step_angle 0.0
end

:PlayDoorSound
3F86: attach_sound "jvt/door.wav" to_car vehicle offset -1.0 0 0 loop 0 min 10.0
return

:StopDoorSound
if
    3F92: is_sound_playing "jvt/door.wav" index vehicle
then
    3F91: stop_sound "jvt/door.wav" index vehicle
end
return