{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
float z_offset
float amount
float vehicle_speed
float x
float y
float z
float dx
float dy
float dz
float jx
float jy
float jz
int engine_state
int ignore

:WheelThrust
wait 0
3F02: engine_state = car vehicle engine_status
if and
    not Car.Wrecked(vehicle)
    not engine_state == 0
jf @WheelThrustEnd
if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
jf @WheelThrustEnd
// Limited amount of time to engage wheel thrusters after hover conversion
if
   timera < 5000 // integer values
jf @WheelThrustEnd
if
00E1:   key_pressed 0 16
jf @WheelThrust
timera = 0 // integer values
3F96: ignore = attach_sound "delorean/wheel_thrust.wav" to_car vehicle offset 0 0 0 loop 0 size 30.0

:WheelThrustCheck
wait 10
// Only thrust for 2 seconds max
if
    not Car.Wrecked(vehicle)
jf @WheelThrustFadeOut
if or
    Actor.InCar($PLAYER_ACTOR, vehicle)
    3F2A:   is_player_in_remote_mode_with_car vehicle
jf @WheelThrustFadeOut
if
   timera < 2000 // integer values
jf @WheelThrustFadeOut

// Unload if we let go of the gas
if
00E1:   key_pressed 0 16
jf @WheelThrustFadeOut

// Vent thruster will override the wheel thruster
if
80E1:   not key_pressed 0 6
jf @WheelThrustFadeOut

// Thrusters
cleo_call @ThrusterParticle 3 vehicle -1.0 2.0
cleo_call @ThrusterParticle 3 vehicle -1.5 2.0
cleo_call @ThrusterParticle 3 vehicle -2.0 2.0
cleo_call @ThrusterParticle 3 vehicle -2.5 1.9
cleo_call @ThrusterParticle 3 vehicle -3.0 1.8
jump @WheelThrustCheck

:ThrusterParticle
gosub @CalculateSpeedOffset

0407: create_coordinate x y z from_car vehicle offset 0.0 7.3 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA 255 100 50 255 lifespan 10
0407: create_coordinate x y z from_car vehicle offset 0.0 3.6 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA 255 100 50 255 lifespan 10
0407: create_coordinate x y z from_car vehicle offset 0.0 0.2 z_offset
gosub @AddSpeedOffset
3F60: scatter_particle 2 amount at x y z 0 0 0 RGBA 255 100 50 255 lifespan 10
cleo_return 0

:CalculateSpeedOffset
3F33: get_car vehicle velocity_direction dx dy dz
02E3: vehicle_speed = car vehicle speed
vehicle_speed /= 30.0
006B: dx *= vehicle_speed
006B: dy *= vehicle_speed
006B: dz *= vehicle_speed
return

:AddSpeedOffset
005B: x += dx
005B: y += dy
005B: z += dz
0208: jx = random_float -0.05 0.05
0208: jy = random_float -0.05 0.05
0208: jz = random_float -0.05 0.05
005B: x += jx
005B: y += jy
005B: z += jz
return

:WheelThrustFadeOut
cleo_call @ThrusterParticle 3 vehicle -1.0 2.0
cleo_call @ThrusterParticle 3 vehicle -1.5 2.0
cleo_call @ThrusterParticle 3 vehicle -2.0 2.0
cleo_call @ThrusterParticle 3 vehicle -2.5 1.9
wait 10
cleo_call @ThrusterParticle 3 vehicle -1.0 2.0
cleo_call @ThrusterParticle 3 vehicle -1.5 2.0
cleo_call @ThrusterParticle 3 vehicle -2.0 2.0
wait 10
cleo_call @ThrusterParticle 3 vehicle -1.0 2.0
cleo_call @ThrusterParticle 3 vehicle -1.5 2.0
wait 10
cleo_call @ThrusterParticle 3 vehicle -1.0 2.0

:WheelThrustEnd
3F10: set_car vehicle component "locothrusteron" visible 0
3F10: set_car vehicle component "bogie1locothrusteron" visible 0
terminate_this_custom_script
